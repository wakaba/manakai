Module:
  @QName: 
    DIS:DISCore
  @FullName:
    @@lang:en
    @@@: 
      <QUOTE::dis> Format Core Module
  @Namespace:
    http://suika.fam.cx/~wakaba/archive/2004/dis/Core#

  @Description:
    @@lang:en
    @@@:
      This module introduces the basic <QUOTE::dis> language 
      vocabulary that is common to <QUOTE::dis> format. 
  
  @Author:
    @@FullName:Wakaba
    @@Mail:w@suika.fam.cx
  @License:
     license:Perl+MPL
  @Date:
    @@@:
      $Date: 2005/09/17 15:03:04 $
    @@ContentType:
      dis:Date.RCS
  
  @DefaultFor:
    ManakaiDOM:all

  @Require:
    @@Module:
      @@@Name: DISRDF

Namespace:
  @DIS:
    http://suika.fam.cx/~wakaba/archive/2004/dis/
  @dis:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#dis--
  @DOMMain:
    http://suika.fam.cx/~wakaba/archive/2004/dom/main#
  @lang:
     http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#
  @license:
     http://suika.fam.cx/~wakaba/archive/2004/8/18/license#
  @ManakaiDOM:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/manakai-dom#
  @owl:
    http://www.w3.org/2002/07/owl#
  @rdf:
    http://www.w3.org/1999/02/22-rdf-syntax-ns#
  @rdfs:
    http://www.w3.org/2000/01/rdf-schema#

## -- Syntax sugar vocabularies

ResourceTypeDef:
  @QName: ElementType
  @Description:
    @@lang:en
    @@@:
      A dis element type.

ResourceDef:
  @QName:
    dis:ElementTypeBinding
  @Description:
    @@lang:en
    @@@:
      Binds a <QUOTE::dis> <CODE::NCName> with global element type name and 
      shadow contents.
      \
      {NOTE:: Scope of <Q::dis:ElementTypeBinding> is limited to 
              the file in which the element occurs.
      \
      }
  @rdf:type: ElementType

ResourceDef:
  @QName:
    dis:ElementType
  @Description:
    @@lang:en
    @@@:
      A global element type to which an <CODE::NCName> is bound to.  
      This type of element is used in <Q::dis:ElementTypeBinding> elements.
  @rdf:type: ElementType
  @dataType:
    dis:TypeQName
  @rdfs:range: ElementType

ResourceDef:
  @QName:
    dis:ShadowContent
  @Description:
    @@lang:en
    @@@:
      A element content value and/or child elements that is appended 
      to elements bound to. 
  @rdf:type: ElementType
  @dataType:
    lang:dis

ResourceDef:
  @QName:
    dis:Namespace
  @Description:
    @@lang:en
    @@@:
      Binds <CODE::NCName>s (i.e. namespace prefixes) to namespace URI 
      references. 
      \
      {NOTE:: Scope of namespace bindings is limited to the file 
              in which the element occurs.
      \
      }
  @rdf:type: ElementType
  @rdf:type: rdf|Property

ElementTypeBinding:
  @Name: ResourceTypeDef
  @ElementType: 
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdfs:Class
    @@ISA:
      rdfs:Class

ElementTypeBinding:
  @Name: PropDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdf:Property

ElementTypeBinding:
  @Name: dataType
  @ElementType:
    dis:dataType
  @ShadowContent:
    @@ContentType: dis|TypeQName

ElementTypeBinding:
  @Name: multipleProperties
  @ElementType:
    dis:multipleProperties
  @ShadowContent:
    @@ContentType: dis|TypeQName

## -- Module group defining

ResourceTypeDef:
  @QName: 
    dis:ModuleGroup
  @Description:
    @@lang:en
    @@@: A set of modules

ResourceDef:
  @QName:
    DIS:
  @rdf:type:
    dis:ModuleGroup
  @Description:
    @@lang:en
    @@@:
      The <QUOTE::dis> core library.

## -- Module defining

ResourceDef:
  @QName:
    dis:Module
  @rdf:type: ElementType
  @Description:
    @@lang:en
    @@@:
      An element that contains information about a <QUOTE::dis> module. 

PropDef:
  @QName:
    dis:Require
  @Description:
    @@lang:en
    @@@: 
      References to external modules.
  @multipleProperties: Single

## -- Resource defining

ResourceDef:
  @QName:
    dis:ResourceDef
  @rdf:type: ElementType
  @Description:
    @@lang:en
    @@@:
      A <Q::dis:ResourceDef> element defines a resource.

ResourceTypeDef:
  @QName: 
    dis:MultipleResource
  @Description:
    @@lang:en
    @@@:
      An abstract resource - it has zero or more concrete representations, 
      that are distinguished by <CODE::for> URI references.
  @AliasFor:
    @@@:
      ::ManakaiDOM:all
    @@For:
      ManakaiDOM:all !=ManakaiDOM:all

PropDef:
  @QName:
    dis:resourceFor
  @rdfs:domain:
    dis:MultipleResource
  @rdfs:range:
    dis:For
  @Description:
    @@lang:en
    @@@:
      A <CODE::for> URI reference that can identity one representation 
      of the resource.
      \
      {NOTE:: To test <CODE::for> declared with <Q::dis:resourceFor> 
              element, use <Q::dis:ForCheck> attribute, rather than
              <Q::dis:For> attribute.  <Q::dis:For> attribute does 
              not check additional <CODE::For> URI references 
              declared with <Q::dis:resourceFor> element.
      \
      }
  @multipleProperties: UnorderedList
  @dataType: dis|TypeQName

## -- Definition for

PropDef:
  @QName:
    dis:For
  @rdfs:domain:
    rdfs:Resource
  @dataType:
    dis:TypeQName
  @Description:
    @@lang:en
    @@@:
      A <Q::For> for which the resource is used (<QUOTE::OR> filter).
  @multipleProperties: UnorderedList

PropDef:
  @QName:
    dis:ForCheck
  @rdfs:domain:
    rdfs:Resource
  @dataType:
    dis:TypeQName
  @Description:
    @@lang:en
    @@@:
      A <Q::For> for which the resource is used (<QUOTE::AND> filter).
  @multipleProperties: UnorderedList

PropDef:
  @QName: dis|DefaultFor
  @Description:
    @@lang:en
    @@@:
      The default <QUOTE::for> for the module.
  @dataType: dis|TypeQName
  @multipleProperties: Single

ForDef:
  @QName:
    ManakaiDOM:all
  @FullName:
    @@lang:en
    @@@: For all

## -- Resource naming

PropDef:
  @QName:
    dis:Name
  @Description:
    @@lang:en
    @@@:
      A name of a resource.
  @dataType: String
  @multipleProperties: Single

PropDef:
  @QName:
    dis:QName
  @Description:
    @@lang:en
    @@@:
      A qualified name of a resource.
  @dataType: dis|TypeQName
  @multipleProperties: Single

PropDef:
  @QName:
    dis:AppName
  @Description:
    @@lang:en
    @@@: An application (binding) dependent name of a resource.
  @dataType: String
  @multipleProperties: UnorderedList

PropDef:
  @QName:
    dis:FullName
  @Description:
    @@lang:en
    @@@:
      A human-readable name of a resource.
  @ISA:
    rdfs:label
  @dataType: lang|disdocInline
  @multipleProperties: Single

PropDef:
  @QName:
    dis:Label
  @Description:
    @@lang:en
    @@@:
      A human-readable simple header of a resource.
  @ISA:
    rdfs:label
  @dataType: lang|disdocInline
  @multipleProperties: Single

PropDef:
  @QName: 
    dis:FileName
  @Description:
    @@lang:en
    @@@:
      A file name.
  @dataType: String
  @multipleProperties: Single

## -- Resource description

PropDef:
  @QName:
    dis:Description
  @Description:
    @@lang:en
    @@@:
      A human-readable description of a resource.
  @ISA:
    rdfs:comment
  @dataType: lang|disdoc
  @multipleProperties: OrderedList

PropDef:
  @QName:
    dis:ImplNote
  @Description:
    @@lang:en
    @@@:
      An implementor's node about resource.
  @dataType: lang|disdoc
  @multipleProperties: OrderedList

PropDef:
  @QName:
    dis:Spec
  @Description:
    @@lang:en
    @@@:
      A reference to a specification.  (Obsolete)
PropDef:
  @QName:
    dis:Level
  @Description:
    @@lang:en
    @@@:
      DOM Level (obsolete)
PropDef:
  @QName:
    dis:SpecLevel
  @Description:
    @@lang:en
    @@@:
      DOM Level of specification (obsolete) - use <CODE::For> instead.
PropDef:
  @QName:
    dis:Condition
  @Description:
    @@lang:en
    @@@:
      Condition (obsolete) - use <CODE::For> instead.

ResourceDef:
  @QName:
    ManakaiDOM:InCase
  @rdf:type:
    rdfs:Class
  @subsetOf:
    rdfs:Class
  @rdfs:subClassOf:
    rdfs:Class
  @Description:
    @@lang:en
    @@@: 
      Cases. 

## -- Resource-to-resource relationships

PropDef:
  @QName: derivedFrom
  @Description:
    @@lang:en
    @@@:
      A subject resource is in some way derived from an object resource. 
  @rdfs:domain:
    rdfs:Resource
  @rdfs:range:
    rdfs:Resource
  @dataType: dis|TFQNames
  @multipleProperties: OrderedList

PropDef:
  @QName:
    dis:subsetOf
  @Description:
    @@lang:en
    @@@:
      A subject resource is a subset of an object resource. 
  @rdfs:domain:
    rdfs:Resource
  @rdfs:range:
    rdfs:Resource
  @subsetOf: derivedFrom
  @rdfs:subPropertyOf:derivedFrom
  @dataType:
    dis:TFQNames
  @multipleProperties: UnorderedList

PropDef:
  @QName:
    dis:supersetOf
  @Description:
    @@lang:en
    @@@:
      The subject resource is a superset of the object resource. 
      \
      {NOTE:: This property is intended for internal use; it should 
              not be directly specified in the <Q::dis> files.
      \
      }
  @dataType: dis|TFQNames
  @multipleProperties: UnorderedList

PropDef:
  @QName:
    dis:ISA
  @Description:
    @@lang:en
    @@@:
      Inheritance relationship of a resource.
  @subsetOf: derivedFrom
  @rdfs:subPropertyOf:derivedFrom
  @dataType:
    dis:TFQNames
  @multipleProperties: OrderedList

PropDef:
  @QName:
    dis:AppISA
  @Description:
    @@lang:en
    @@@:
      Application dependent is-a relationship.
  @subsetOf: derivedFrom
  @rdfs:subPropertyOf:derivedFrom
  @multipleProperties: OrderedList
  @dataType: String

PropDef:
  @QName:
    dis:Implement
  @Description:
    @@lang:en
    @@@:
      <QUOTE::Implementing> relationship of a resource.
  @subsetOf:derivedFrom
  @rdfs:subPropertyOf:derivedFrom
  @dataType:
    dis:TFQNames
  @multipleProperties: UnorderedList

PropDef:
  @QName:
    dis:AliasFor
  @Description:
    @@lang:en
    @@@:
      Makes the subject an alias for another resource. 
      \
      {NOTE:: Although its semantics is almost same as 
              <Q::owl:sameAs>, this property carries 
              an additional meaning that the object has 
              a <QUOTE::superior> or canonical name to the subject's. 
      \
      }
  @rdfs:subClassOf:
    owl:sameAs
  @subsetOf:
    owl:sameAs
  @rdfs:domain:
    rdfs:Resource
  @rdfs:range:
    rdfs:Resource
  @dataType:
    dis:TFQNames
  @multipleProperties: Single

PropDef:
  @QName:
    dis:aliasChild
  @Description:
    @@lang:en
    @@@:
      A <QUOTE::dis> processing instruction whether the subject resource 
      should be interpreted as a resource when the subject resource 
      is described as an <Q::dis:aliasFor> another resource.  Any 
      <Q::dis:ResourceDef> that is a child of alias <Q::dis:ResourceDef> 
      is not processed when a <QUOTE::dis> file 
      is compiled, unless <Q::dis:aliasChild> is <CODE::1>. 
  @rdfs:domain:
    dis:ResourceDef
  @dataType: 
    dis:Boolean
  @multipleProperties: Single

PropDef:
  @QName:
    ManakaiDOM:isRedefining
  @dataType:
    dis:Boolean
  @Description:
    @@lang:en
    @@@:
      Whether the subject resource (such as method or attribute) 
      redefines another resource defined in one of ancestor inheriting 
      class or not.
  @multipleProperties: Single

PropDef:
  @QName:
    dis:IsAbstract
  @Description:
    @@lang:en
    @@@:
      Whether the resource is defined abstractly or concretly. 
      Abstractly defined resource must be concretly overloaded 
      by inheriting class.
  @dataType: dis|Boolean
  @multipleProperties: Single

PropDef:
  @QName:
    dis:IsFinal
  @Description: 
    @@lang:en
    @@@:
      Whether a subject resource is overridable or not. 
      \
      - <CODE::-1> :: The subject resource have to be overrided.
      \
      - <CODE::0>  :: The subject resource is able to be overrided. 
      \
      - <CODE::1>  :: The subject resource is unable to be overrided.
  @dataType:
    DISCore:Tristate
  @multipleProperties: Single

PropDef:
  @QName: stopISARecursive
  @Description:
    @@lang:en
    @@@:
      Whether the <Q::dis:ISA>-recursive property search
      (i.e. <M::DIS:ManakaiDISPropertyAccessor.getPropertyResourceList>)
       should be stopped or not.
  @dataType:
    dis:Boolean
  @multipleProperties: Single

## --- Datatype

PropDef:
  @QName:
    dis:dataType
  @Description:
    @@lang:en
    @@@:
      Lexical data type of the property object.
  @rdfs:domain:
    rdf:Property
  @rdfs:range:
    rdf:Property
  @dataType:
    dis:TypeQName
  @multipleProperties: Single

ResourceDef:
  @QName: AnyType
  @rdf:type:
    rdfs:Class
  @subsetOf:
    rdfs:Class
  @rdfs:subClassOf:
    rdfs:Class
  @Description:
    @@lang:en
    @@@:
      A data type, including programming language data types, 
      programming language classes, markup language data types, 
      string formats, and so on. 

ResourceDef:
  @QName: AbstractDataType
  @AliasFor: AnyType
  @enDesc: 
    The name of <QUOTE::abstract data type> was incorrect.

ResourceDef:
  @QName: LexicalType
  @rdf:type:
    rdfs:Class
  @subsetOf: AnyType
  @rdfs:subClassOf: AnyType
  @Description:
    @@lang:en
    @@@:
      Lexical data formats.

ResourceDef:
  @QName: StringDataType
  @AliasFor: LexicalType

ElementTypeBinding:
  @Name: DataTypeDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdfs:Datatype

ResourceDef:
  @QName: 
    ManakaiDOM:DataType
  @Description:
    @@lang:en
    @@@:
      A programming language data type.
  @rdf:type:
    rdfs:Class
  @subsetOf: AnyType
  @rdfs:subClassOf: AnyType

PropDef:
  @QName:
    dis:ContentType
  @rdfs:domain:
    DOMMain:any
  @rdfs:range: LexicalType
  @dataType:
    dis:TypeQName
  @Description:
    @@lang:en
    @@@:
      The media type (content type) of the content.
  @multipleProperties: Single

PropDef:
  @QName:
    dis:lang
  @rdfs:domain:
    rdfs:Resource
  @Description:
    @@lang:en
    @@@:
      The natural language in which the content is written.
  @dataType: LanguageTag
  @multipleProperties: Single

PropDef:
  @QName:
    dis:script
  @rdfs:domain:
    rdfs:Resource
  @Description:
    @@lang:en
    @@@:
      The writting script in which the content is written.
  @dataType: ScriptTag
  @multipleProperties: Single

DataTypeDef:
  @QName:
    lang:disdoc
  @FullName:
    @@lang:en
    @@@: 
      The <QUOTE::disdoc> text format

DataTypeDef:
  @QName: 
    lang:disdocInline
  @FullName:
    @@lang:en
    @@@:
      The <QUOTE::disdoc> text format, inline context

DataTypeDef:
  @QName: NCName
  @FullName:
    @@lang:en
    @@@:
      <QUOTE::dis> NCName
  @subsetOf: String
  @rdfs:subClassOf: String

DataTypeDef:
  @QName: NamespacePrefix
  @FullName:
    @@lang:en
    @@@:
      <QUOTE::dis> Namespace Prefix
  @subsetOf: NCName
  @rdfs:subClassOf: NCName

DataTypeDef:
  @QName: LocalName
  @FullName:
    @@lang:en
    @@@:
      <QUOTE::dis> Local Name
  @subsetOf: NCName
  @rdfs:subClassOf: NCName
  @subsetOf: dis|TypeQName
  @rdfs:subClassOf: dis|TypeQName

DataTypeDef:
  @QName:
    dis:TypeQName
  @FullName:
    @@lang:en
    @@@: 
      <QUOTE::dis> QName
  @Description:
    @@lang:en
    @@@:
      <QUOTE::dis> QName (qualified name).
      \
      <Q::dis:TypeQName> := [prefix ":"] local-name
      \
      prefix := *(<lt>any character> - ":")
      \
      local-name := *(<lt>any character> - ":")

DataTypeDef:
  @QName: NCNameOrQName
  @Description:
    @@lang:en
    @@@:
      A QName with or without <CHAR::COLON>.  If
      it does not contain a <CHAR::COLON>, it is namespace-unaware
      or in the null namespace.
  @subsetOf: dis|TypeQName
  @rdfs:subClassOf: dis|TypeQName

DataTypeDef:
  @QName: LanguageTag
  @Description:
    @@lang:en
    @@@:
      A qualified name whose default namespace bound to
      the language tag namespace.  See <Module::DIS:NaturalLanguage> module.
  @rdfs:subClassOf:
    dis:TypeQName
  @subsetOf:
    dis:TypeQName

DataTypeDef:
  @QName: ScriptTag
  @Description:
    @@lang:en
    @@@:
      A qualified name whose default namespace bound to
      the script tag namespace.  See <Module::DIS:NaturalLanguage> module.
  @rdfs:subClassOf:
    dis:TypeQName
  @subsetOf:
    dis:TypeQName

DataTypeDef:
  @QName: 
    dis:TFQNames
  @Description:
    @@lang:en
    @@@:
      A pair of resource QName and optional <QUOTE::For> QName.

DataTypeDef:
  @QName: TFPQNames
  @Description:
    @@lang:en
    @@@:
      A triplet of resource QName and optional <QUOTE::For> QName
      and zero or more <QUOTE::For+> QNames.

DataTypeDef:
  @QName: URI
  @Description:
    @@lang:en
    @@@:
      Any string that identifies a resource.
  @subsetOf: String
  @rdfs:subClassOf: String

DataTypeDef:
  @QName: TFURI
  @Description:
    @@lang:en
    @@@:
      <Q::dis:TFQNames>-expanded URI references. 
  @rdfs:subClassOf: URI
  @subsetOf: URI

DataTypeDef:
  @QName:
    dis:Boolean
  @FullName:
    @@lang:en
    @@@: Boolean truth
  @Description:
    @@lang:en
    @@@: 
      A boolean truth value, i.e. true or false. 
      \
      <Q:dis:Boolean> := "1" / "0"

DataTypeDef:
  @Name: Tristate
  @FullName:
    @@lang:en
    @@@: Tristate value
  @Description:
    @@lang:en
    @@@:
      A tristate value. 
      \
      <Q::Tristate> := "-1" / "0" / "1".
      \
      {NOTE:: What a <Q::Tristate> value means depends on the 
              context where the value is used. 
      \
      }

DataTypeDef:
  @Name: String
  @FullName:
    @@lang:en
    @@@: String

DataTypeDef:
  @QName:
    dis:String
  @AliasFor: String

PropDef:
  @QName: dis|Author
  @FullName:
    @@lang:en
    @@@: Author
  @dataType: String
  @multipleProperties: OrderedList

PropDef:
  @QName: dis|Mail
  @FullName:
    @@lang:en
    @@@: Internet mail address
  @dataType: String
  @multipleProperties: UnorderedList

PropDef:
  @QName: dis|Date
  @FullName:
    @@lang:en
    @@@: Date
  @dataType: dis|Date.RCS
  @multipleProperties: Single
  
DataTypeDef:
  @QName:
    dis:Date.RCS
  @FullName:
    @@lang:en
    @@@: RCS tag-style date and time specification

## -- Properties

PropDef:
  @QName: dis|multipleProperties
  @Description:
    @@lang:en
    @@@:
      This attribute defines that how the values should be interpreted
      if a property is described as multiple attributes in
      <QUOTE::dis> source tree.
  @rdfs:domain: rdf|Property
  @rdfs:range: rdfs|Resource
  @dataType: dis|TypeQName
  @multipleProperties: Single

ResourceDef:
  @QName: OrderedList
  @Description:
    @@lang:en
    @@@:
      If multiple values are specified to a property, then
      they makes up an ordered collection of values in document
      order.
  @rdf:type: rdfs|Resource

ResourceDef:
  @QName: UnorderedList
  @Description:
    @@lang:en
    @@@:
      If multiple valus are specified to a property, then
      they makes up an unordered collection of values.
  @rdf:type: rdfs|Resource

ResourceDef:
  @QName: SingleValue
  @Description:
    @@lang:en
    @@@:
      Only a value is allowed for the property; if there
      are more than one attributes in the source tree, it is an error.
  @rdf:type: rdfs|Resource

## -- Definitions

PropDef:
  @QName:
    dis:Value
  @Description:
    @@lang:en
    @@@:
      A value of the resource.
  @dataType: lang|dis
  @multipleProperties: Single

PropDef:
  @QName:
    dis:DefaultValue
  @Description:
    @@lang:en
    @@@: The default value of the resource.
  @dataType: String
  @multipleProperties: Single

PropDef:
  @QName: dis|is-null
  @Description:
    @@lang:en
    @@@:
      Whether the value is <DOM::null> or not.
  @FullName:
    @@lang:en
    @@@:
      Is <DOM::null> value
  @dataType: dis|Boolean
  @multipleProperties: Single

PropDef:
  @QName:
    dis:Def
  @Description:
    @@lang:en
    @@@:
      A definition of the resource.
  @dataType: String
  @multipleProperties: OrderedList

ResourceTypeDef:
  @QName: AnyDefContainer
  @Description:
    @@lang:en
    @@@:
      Resources which have one or more <Q::dis:Def>s.

## -- Access controls

PropDef:
  @QName:
    ManakaiDOM:isForInternal
  @dataType:
    dis:Boolean
  @Description:
    @@lang:en
    @@@:
      Whether the subject resource is prepared for the internal use.  
      In other word, the subject resource is a public member or 
      a private member.
  @multipleProperties: Single

## -- Status

PropDef:
  @QName: isDeprecated
  @dataType:
    dis:Boolean
  @Description:
    @@lang:en
    @@@:
      Whether the use of the subject resource is deprecated or not. 
  @multipleProperties: Single

PropDef:
  @QName: alt
  @dataType:
    dis:TFQNames
  @Description:
    @@lang:en
    @@@:
      Another resource the subject resource is deprecated in favor of. 
      \
      This property is specified as a child element of 
      <Q::isDeprecated> property element.
  @multipleProperties: OrderedList

## -- License

## TODO: Move to separate module

PropDef:
  @QName:
    dis:License
  @Description:
    @@lang:en
    @@@:
      License term.
  @dataType: 
    dis:TypeQName
  @multipleProperties: Single

ResourceDef:
  @QName:
    license:Perl+MPL
  @rdf:type:
    rdfs:Resource
  @Description:
    @@lang:en
    @@@:
      <QUOTE::Same as Perl> or <QUOTE::MPL 1.1/GPL 2.0/LGPL 2.1> license. 

PropDef:
  @QName: dis|Original
  @FullName:
    @@lang:en
    @@@:
      Original work
  @multipleProperties: OrderedList

PropDef:
  @QName: dis|Year
  @multipleProperties: Single
