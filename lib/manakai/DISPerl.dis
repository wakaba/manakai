Module:
  @QName: 
    DIS:DISPerl
  @FullName:
    @@lang:en
    @@@: 
      <QUOTE::dis> Perl Support Module
  @Namespace:
    http://suika.fam.cx/~wakaba/archive/2004/dis/Perl#
  
  @Author:
    @@FullName:Wakaba
    @@Mail:w@suika.fam.cx
  @License:
     license:Perl+MPL
  @Date:
    @@@:
      $Date: 2005/09/17 15:03:04 $
    @@ContentType:
      dis:Date.RCS
  
  @DefaultFor:
    ManakaiDOM:all

  @Require:
    @@Module:
      @@@Name: DISCore
    @@Module:
      @@@Name: DISLang

Namespace:
  @DIS:
    http://suika.fam.cx/~wakaba/archive/2004/dis/
  @dis:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#dis--
  @dis2pm:
    http://suika.fam.cx/~wakaba/archive/2004/11/8/dis2pm#
  @disPerl:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#dis--Perl--
  @DOMMain:
    http://suika.fam.cx/~wakaba/archive/2004/dom/main#
  @html5:
    http://www.w3.org/1999/xhtml
  @lang:
     http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#
  @license:
     http://suika.fam.cx/~wakaba/archive/2004/8/18/license#
  @ManakaiDOM:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/manakai-dom#
  @owl:
    http://www.w3.org/2002/07/owl#
  @Perl:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#Perl--
  @rdf:
    http://www.w3.org/1999/02/22-rdf-syntax-ns#
  @rdfs:
    http://www.w3.org/2000/01/rdf-schema#

ElementTypeBinding:
  @Name: DataTypeDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISCore|AnyType

ElementTypeBinding:
  @Name: FormatDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISCore|LexicalType
    @@rdfs:subClassOf:
      DISCore:String

ElementTypeBinding:
  @Name: PropDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdf:Property

## -- For Perl

ForDef:
  @QName:
    ManakaiDOM:Perl
  @FullName:
    @@lang:en
    @@@: For Perl 5 binding

## -- Perl datatypes

FormatDef:
  @QName:
    lang:Perl
  @FullName:
    @@lang:en
    @@@: Perl 5 code

DataTypeDef:
  @QName:
    Perl:Scalar
  @FullName:
    @@lang:en
    @@@: Perl Scalar Value

DataTypeDef:
  @QName: String
  @FullName:
    @@lang:en
    @@@: Perl String Value
  @rdfs:subClassOf: DISLang|String
  @subsetOf: DISLang|String
  @rdfs:subClassOf: Perl|Scalar
  @subsetOf: Perl|Scalar

DataTypeDef:
  @QName: Boolean
  @FullName:
    @@lang:en
    @@@: Perl Boolean Value
  @rdfs:subClassOf: Perl|Scalar
  @subsetOf: Perl|Scalar

DataTypeDef:
  @QName: Reference
  @FullName:
    @@lang:en
    @@@: Perl reference
  @rdfs:subClassOf: 
    Perl:Scalar
  @subsetOf: Perl|Scalar

DataTypeDef:
  @QName:
    Perl:SCALAR
  @FullName:
    @@lang:en
    @@@: Perl reference to scalar
  @rdfs:subClassOf: Reference

DataTypeDef:
  @QName:
    Perl:Array
  @FullName:
    @@lang:en
    @@@: Perl Array

DataTypeDef:
  @QName:
    Perl:ARRAY
  @FullName:
    @@lang:en
    @@@: Perl array reference
  @rdfs:subClassOf: Reference

DataTypeDef:
  @QName:
    Perl:Hash
  @FullName:
    @@lang:en
    @@@: Perl Hash

DataTypeDef:
  @QName:
    Perl:HASH
  @FullName:
    @@lang:en
    @@@: Perl hash reference
  @rdfs:subClassOf: Reference

ResourceDef:
  @QName: Perl|hash
  @AliasFor: Perl|HASH
  @ImplNote:
    @@lang:en
    @@@:
      <Q::Perl:hash> is obsolete; use <Q::Perl:HASH> instead.

DataTypeDef:
  @QName:
    Perl:CODE
  @FullName:
    @@lang:en
    @@@: Perl code reference
  @rdfs:subClassOf: Reference

## -- Perl constructs

ResourceDef:
  @QName: ScalarVariable
  @rdf:type:
    rdfs:Class
  @rdfs:subClassOf:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:
      Scalar variable

ResourceDef:
  @QName: AsStringMethod
  @rdf:type:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:
      Methods to searialize an object as a string. 

ResourceDef:
  @QName: NewMethod
  @rdf:type:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:
      Methods to construct an instance of a class. 

ResourceDef:
  @QName:CloneMethod
  @rdf:type:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:Methods to make a clone of an object. 

ResourceDef:
  @QName: ImportMethod
  @rdf:type:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:
      The <Perl::import> method.

ResourceDef:
  @QName: UnimportMethod
  @rdf:type:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:
      The <Perl::unimport> method.

ResourceDef:
  @QName: ISAMethod
  @rdf:type:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:
      The <Perl::isa> method.

ResourceDef:
  @QName: CanMethod
  @rdf:type:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:
      The <Perl::can> method.

ResourceDef:
  @QName: VERSIONMethod
  @rdf:type:
    rdfs:Class
  @FullName:
    @@lang:en
    @@@:
      The <Perl::VERSION> method.

## -- Perl construct properties

PropDef:
  @QName: isExportOK
  @Description:
    @@lang:en
    @@@:
      Whether a subject resource should be listed in <Perl::@EXPORT_OK> 
      or not.
  @dataType:
    dis:Boolean
  @multipleProperties: DISCore|Single

PropDef:
  @QName: isNamedParameter
  @Description:
    @@lang:en
    @@@:
      Whether the method parameter should be passed by named parameter 
      (hash) style or not. 
  @dataType:
    dis:Boolean
  @rdfs:domain:
    DISLang:MethodParameter
  @multipleProperties: DISCore|Single

PropDef:
  @QName: defaultValueHasSelfReference
  @Description:
    @@lang:en
    @@@:
      Whether the <Q::dis:DefaultValue> has a reference to itself or not.
  @dataType:
    dis:Boolean
  @multipleProperties: DISCore|Single

## -- Perl names

FormatDef:
  @QName:
    Perl:package-name
  @FullName:
    @@lang:en
    @@@:
      Perl package name
  @Description:
    @@lang:en
    @@@:
      A fully qualified Perl package name. 
  @rdfs:subClassOf:
    lang:Perl

PropDef:
  @QName: interfacePackage
  @FullName:
    @@lang:en
    @@@:
      Perl interface package name
  @rdfs:subPropertyOf: dis|AppName
  @dataType: DISCore|String
  @multipleProperties: DISCore|Single

PropDef:
  @QName: classPackage
  @FullName:
    @@lang:en
    @@@:
      Perl class package name
  @rdfs:subPropertyOf: dis|AppName
  @dataType: DISCore|String
  @multipleProperties: DISCore|Single

FormatDef:
  @QName: SubName
  @FullName:
    @@lang:en
    @@@: Perl subroutine name
  @rdfs:subClassOf:
    lang:Perl

FormatDef:
  @QName: MethodName
  @FullName:
    @@lang:en
    @@@:
      Perl method name
  @Description:
    @@lang:en
    @@@:
      Perl (unqualified) method names. 
  @rdfs:subClassOf: SubName

FormatDef:
  @QName: SubPrototype
  @FullName:
    @@lang:en
    @@@:
      Perl subroutine prototype 
  @rdfs:subClassOf:
    lang:Perl

## -- Perl binding specific properties and datatypes

PropDef:
  @QName:
    ManakaiDOM:moduleSuffix
  @Description:
    @@lang:en
    @@@:
      A string suffixing module names.
  @dataType: DISCore|String
  @multipleProperties: DISCore|Single

FormatDef:
  @QName: MemRef
  @Description:
    @@lang:en
    @@@:
      A reference to a member of a class. 
      \
      MemRef := [dis:TFQNames] "." NCName
      \
      The optional <Q::dis:TFQNames> identifiers the class to which the 
      member referred belongs.  The NCName is the name of 
      the member (either a method name or a capitalized attribute 
      name prefixed with "get" or "set").

## -- The dis:Perl tree language vocabulary

DataTypeDef:
  @QName:
    dis:Perl
  @FullName:
    @@lang:en
    @@@: 
     <QUOTE::dis> tree fragment generating Perl code
  @rdfs:subClassOf: 
    lang:dis

ResourceDef:
  @Name: TreeElementType
  @rdf:type:
    rdfs:Class
  @rdfs:subClassOf:
    DISLang:TreeElementType
  @Description:
    @@lang:en
    @@@:
      A <Q::dis:Perl> tree vocabulary element type.

ElementTypeBinding:
  @Name: TreeElementType
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: TreeElementType

TreeElementType:
  @Name: cloneCode
  @Description:
    @@lang:en
    @@@:
      Copies definition from other resource.
  @dataType: MemRef

## -- The dis:Perl block preprocessing vocabulary

ResourceDef:
  @Name: BlockElementType
  @rdf:type:
    rdfs:Class
  @rdfs:subClassOf:
    rdfs:Class
  @Description:
    @@lang:en
    @@@:
      A <Q::dis:Perl> block preprocessing vocabulary element type.

ElementTypeBinding:
  @Name: BlockElementType
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: BlockElementType

BlockElementType:
  @QName:
    disPerl:ASSERT
  @Description:
    @@lang:en
    @@@:
      An assertion. 

BlockElementType:
  @QName:
    disPerl:CODE
  @Description:
    @@lang:en
    @@@:
      A block code fragment. 
  @dataType:
    dis:TypeQName

BlockElementType:
  @QName:
    disPerl:DEEP
  @Description:
    @@lang:en
    @@@:
      Sets the method call stack level deeper.  Currently it is 
      used to set the depth for reporting the position where
      an exception has thrown.

BlockElementType:
  @QName:
    disPerl:UNDEEP
  @Description:
    @@lang:en
    @@@:
      Sets the method call stack level shallwer. 

FormatDef:
  @QName: XWSpecification
  @Description:
    @@lang:en
    @@@:
      A reference to an exception or warning code and 
      optional parameter specification list. 

BlockElementType:
  @QName:
    disPerl:EXCEPTION
  @Description:
    @@lang:en
    @@@:
      Throws an exception. 
  @dataType: XWSpecification

BlockElementType:
  @QName:
    disPerl:WARNING
  @Description:
    @@lang:en
    @@@:
      Reports a warning. 
  @dataType: XWSpecification

BlockElementType:
  @QName:
    disPerl:FOR
  @Description:
    @@lang:en
    @@@:
      <QUOTE::For> selection.

BlockElementType:
  @QName: 
    disPerl:WHEN
  @Description:
    @@lang:en
    @@@:
      Conditional section.  This element type is 
      deprecated in favor of <Q::disPerl::FOR>.  <Q::Util:DIS>
      module no longer support <Q::disPerl:WHEN> while
      <FILE::cdis2pm.pl> implements it. 

BlockElementType:
  @QName: 
    disPerl:FILE
  @Description:
    @@lang:en
    @@@:
       The name of source file. 

BlockElementType:
  @QName:
    disPerl:LINE
  @Description:
    @@lang:en
    @@@:
      The line number in source code.

BlockElementType:
  @QName:
    disPerl:PACKAGE
  @Description:
    @@lang:en
    @@@:
      The current Perl package name. 

## -- The dis:Perl inline preprocessing vocabulary

ResourceDef:
  @Name: InlineElementType
  @rdf:type:
    rdfs:Class
  @rdfs:subClassOf:
    rdfs:Class
  @Description:
    @@lang:en
    @@@:
      A <Q::dis:Perl> inline preprocessing vocabulary element type.

ElementTypeBinding:
  @Name: InlineElementType
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: InlineElementType

InlineElementType:
  @QName:
    disPerl:Q
  @Description:
    @@lang:en
    @@@:
      A qualified name (QName).  It is replaced by a string literal 
      it value is a URI reference expanded from the QName.
  @dataType: 
    dis:TypeQName

InlineElementType:
  @QName:
    disPerl:M
  @dataType: MemRef
  @Description:
    @@lang:en
    @@@:
      A method (object method) call. 

InlineElementType:
  @QName:
    disPerl:ClassM
  @dataType: MemRef
  @Description:
    @@lang:en
    @@@:
      A class method call. 

InlineElementType:
  @QName:
    disPerl:AG
  @Description:
    @@lang:en
    @@@:
      An attribute getting.
  @dataType: MemRef

InlineElementType:
  @QName:
    disPerl:AS
  @Description:
    @@lang:en
    @@@:
      An attribute setting.
  @dataType: MemRef

InlineElementType:
  @QName:
    disPerl:Class
  @dataType:
    dis:TFQNames
  @Description:
    @@lang:en
    @@@:
      A class package specification. 

InlineElementType:
  @QName:
    disPerl:ClassName
  @dataType:
    dis:TFQNames
  @Description:
    @@lang:en
    @@@:
      A class package name string. 

InlineElementType:
  @QName:
    disPerl:IF
  @dataType:
    dis:TFQNames
  @Description:
    @@lang:en
    @@@:
      An interface package specification. 

InlineElementType:
  @QName:
    disPerl:IFName
  @dataType:
    dis:TFQNames
  @Description:
    @@lang:en
    @@@:
      An interface package name. 

InlineElementType:
  @QName:
    disPerl:C
  @dataType: MemRef
  @Description:
    @@lang:en
    @@@: 
      A constant value (not a constant itself - replaced by its value).

InlineElementType:
  @QName:
    disPerl:Code
  @dataType:
    dis:TypeQName
  @Description:
    @@lang:en
    @@@:
      An inline code fragment (<Q::dis2pm:InlineCode>).

## -- Predefined code templates

ResourceTypeDef:
  @QName:
    dis2pm:InlineCode
  @Description:
    @@lang:en
    @@@:
      An inline code fragment referred by <CODE::<lt>CODE::<VAR::qname><gt>> 
      preprocessed token.
      \
      {NOTE:: If a code fragment is written in Perl (i.e. typed as 
              <Q::dis:Perl> or its subtypes), line breaks are 
              replaced by a <CHAR::SPACE>.  Use escape (<Perl::\n>) if 
              it is significant.
      \
      }
  @rdfs:subClassOf:
    DISCore:AnyDefContainer

ResourceTypeDef:
  @QName:
    dis2pm:BlockCode
  @Description:
    @@lang:en
    @@@:
      A block code fragment referred by <CODE::__CODE{<VAR::qname>}__> 
      preprocessed block.
  @rdfs:subClassOf:
    DISCore:AnyDefContainer

ResourceTypeDef:
  @QName: CodeParameter
  @Description:
    @@lang:en
    @@@:
      Paramaters for <Q::dis2pm:BlockCode> or <Q::dis2pm:InlineCode>.
      \
      {P:: A <Q::CodeParameter> resource should have properties:
          
          \
          - <Q::dis:Name> or <Q::dis:QName>::: Parameter name.
                             Namespaces are ignored.
          \
          - <Q::dis:Type>::: Parameter value datatype. 
          \
          - <Q::dis:In>::: Whether the caller sets a value or not. 
                           The default is <CODE::1> (true).
          \
          - <Q::dis:Out>::: Whether the callee sets a value or not. 
                            The default is <CODE::0> (false).
          \
          - <Q::paramStyle>::: Value type of parameter. 
                            The default is <CODE::any>.
          \
      }

PropDef:
  @QName: paramStyle
  @Description:
    @@lang:en
    @@@:
      How parameter value should be specified.
  @dataType: ParamStyleValue

ResourceTypeDef:
  @QName: ParamStyleValue
  @Description:
    @@lang:en
    @@@:
      Possible values for <Q::paramStyle> property.
      \
      - <CODE::any>::: Any type of value.
      \
      - <CODE::var>::: Variable. 

## -- Assertion types for disPerl:ASSERT

ResourceDef:
  @Name: AssertionType
  @rdf:type:
    rdfs:Class
  @rdfs:subClassOf:
    rdfs:Class
  @Description:
    @@lang:en
    @@@:
      Assertion types.

ElementTypeBinding:
  @Name: AType
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: AssertionType

AType:
  @QName:isPositive
  @Description:
    @@lang:en
    @@@:
      Ensures the value is a positive. 
      \
      {NOTE:: Whether the value is a valid number or not is not a matter; 
              this type ensures that the value is assumed as a positive 
              value by Perl. 
      \
      }

AType:
  @QName:invariant
  @Description:
    @@lang:en
    @@@:
      The assertion is always fails.  It is intended to be used 
      for flow invariant, e.g.: 
      \
      if ($mode) {
        # ...
      } else {
        __ASSERT{DISPerl:invariant::
          msg => {qq{Mode "$mode" should be a true value}},
        }__;
      } 

ElementTypeBinding:
  @Name: ResourceTypeDef
  @ElementType: 
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdfs:Class
    @@rdfs:subClassOf:
      rdfs:Class
    @@subsetOf:
      rdfs:Class

ElementTypeBinding:
  @Name: Param
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:MethodParameter

## -- Interface for standard Perl modules

ICDef:
  @IFQName: UNIVERSALInterface
  @ClsQName: UNIVERSAL

  @IFPackName: UNIVERSAL
  @ClsPackName: UNIVERSAL

  @IFFileName: UNIVERSAL-interface
  @ClsFileName: UNIVERSAL

  @enDesc:
    The <Class::UNIVERSAL> class is a base class for <EM::all>
    classes (blessed references).

  @Method:
    @@Operator:
      @@@@: ISAMethod
      @@@ContentType: dis|TypeQName
    @@enDesc:
      Returns whether the class or object is an instance of a class or interface.
    @@Param:
      @@@Name: typeArg
      @@@Type: DISLang|String
      @@@enDesc:
        The name of the package.
    @@Return:
      @@@Type: DOMMain|boolean||ManakaiDOM|all
      @@@trueCase:
        @@@@enDesc:
          The class or object is an instance of <P::typeArg>.
      @@@falseCase:
        @@@@enDesc:
          The class or object is <EM::not> an instance of <P::typeArg>.
  @Method:
    @@Operator:
      @@@@: CanMethod
      @@@ContentType: dis|TypeQName
    @@enDesc:
      Checks if the object or class has a method.
      
      {NOTE:: This method does not function for manakai interfaces,
              since they does not have method definitions in source code.
      }

      {NOTE:: This method cannot check whether an object will be
              able to provide a method through <Perl::AUTOLOAD>.
      }
    @@Param:
      @@@Name: methodArg
      @@@Type: DISLang|String
      @@@enDesc: The name of the method.
    @@Return:
      @@@Type: Perl|CODE
      @@@enDesc:
        A reference to the <Perl::sub>.
      @@@nullCase:
        @@@@enDesc:
          There is no method called <P::methodArg>.
  @Method:
    @@Operator:
      @@@@: VERSIONMethod
      @@@ContentType: dis|TypeQName
    @@enDesc:
      Returns the package version, i.e. the value of the variable
      <Perl::$VERSION> in the package the object is blessed into.
    @@Param:
      @@@Name: requireArg
      @@@Type: DOMMain|any
      @@@enDesc:
        The required version.  It will be compared to the package version
        and if the package version is not greater than or equal
        to <P::requireArg>, then <Perl::die>s.
      @@@nullCase:
        @@@@enDesc:
          No comparison and <Perl::die>ing is done.
    @@Return:
      @@@Type: DOMMain|any
      @@@enDesc:
        The value of the <Perl::$VERSION>.
      @@@nullCase:
        @@@@enDesc: 
          No package version information.
##UNIVERSAL

CDef:
  @ClsQName: Exporter

  @ClsPackName: Exporter

  @enDesc:
    The <Class::Exporter> module implements default <Perl::import>
    method for modules.

  @Method:
    @@Operator:
      @@@@: ImportMethod
      @@@ContentType: dis|TypeQName
    @@Param:
      @@@Name: names
      @@@Type: Perl|Array
      @@@enDesc:
        A list of names of functions, variables and / or tags to export.
    @@Return:
##Exporter

CDef:
  @ClsQName: TieArray

  @IFPackName: 
    Tie::Array
  @ClsPackName:
    Tie::Array

  @enDesc:
    The <Perl::Tie::Array> module is a base class for tied arrays.

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: TIEARRAY
      @@@ContentType: lang|Perl
    @@ManakaiDOM:isStatic:1
    @@enDesc:
      Associates an array instance with the class.
    @@Param:
      @@@Name: list
      @@@Type: Perl|Array
      @@@enDesc:
        Additional arguments needed to complete the association.
    @@Return:
      @@@Type: DOMMain|Object||ManakaiDOM|all
      @@@enDesc:
        An object of a class which provides the other methods.
  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: STORE
      @@@ContentType: lang|Perl
    @@enDesc:
      Stores datum <P::value> into <P::index> for the tied array
      associated with the object.  If this makes the array larger
      then class's mapping of <DOM::null> should be returned
      for new positions.
    @@Param:
      @@@Name: index
      @@@Type: DOMMain|long
      @@@enDesc:
        The position to store <P::value>.
    @@Param:
      @@@Name: value
      @@@Type: DOMMain|any
      @@@enDesc:
        The data to store.
     @@Return:
  
  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: FETCH
      @@@ContentType: lang|Perl
    @@enDesc:
      Retrieves the datum in <P::index> for the tied array
      associated with the object.
    @@Param:
      @@@Name: index
      @@@Type: DOMMain|long
      @@@enDesc:
        The index in the array.
    @@Return:
      @@Type: DOMMain|any
      @@enDesc:
        The data at <P::index>.

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: FETCHSIZE
      @@@ContentType: lang|Perl
    @@enDesc:
      Returns the total number of items in the tied array
      associated with the object, i.e. <Perl::scalar <html5:var::@array>>.
    @@Return:
      @@@Type: DOMMain|long
      @@@enDesc:
        The number of items.

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: STORESIZE
      @@@ContentType: lang|Perl
    @@enDesc:
      Sets the total number of items in the tied array
      associated with the object.  If this makes the array
      larger then class's mapping of <DOM::null> should
      be returned for new positions.  If the array becomes
      smaller then entries beyond <P::count> should be removed.
    @@Param:
      @@@Name: count
      @@@Type: DOMMain|long
      @@@enDesc:
        The new total number of items.
    @@Return:

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: EXTEND
      @@@ContentType: lang|Perl
    @@enDesc:
      Tells that the array is likely to grow to have <P::count>
      entries.  This information can be used to optimize allocation.
      
      This method need do nothing.
    @@Param:
      @@@Name: count
      @@@Type: DOMMain|long
      @@@enDesc: New size.
    @@Return:

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: EXISTS
      @@@ContentType: lang|Perl
    @@enDesc:
      Verifies that the <P::key>th item exists in the tied
      array associated with the object.
    @@Param:
      @@@Name: key
      @@@Type: DOMMain|long
      @@@enDesc: The index of the item.
    @@Return:
      @@@Type: DOMMain|boolean
      @@@trueCase:
        @@@@enDesc:
          There is <P::key>th item.
      @@@falseCase:
        @@@@enDesc:
          There is <EM::no> <P::key>th item.

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: DELETE
      @@@ContentType: lang|Perl
    @@enDesc:
      Removes the <P::key>th item from the tied array associated
      with the object.
    @@Param:
      @@@Name: key
      @@@Type: DOMMain|long
      @@@enDesc:
        The index of the item.
    @@Return:

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: CLEAR
      @@@ContentType: lang|Perl
    @@enDesc:
      Clears all values from the tied array assocaited with the object.
    @@Return:

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: DESTROY
      @@@ContentType: lang|Perl
    @@enDesc:
      Normal object destructor method.
    @@Return:

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: PUSH
      @@@ContentType: lang|Perl
    @@enDesc:
      Appends items to the tied array associated with the object.
    @@Param:
      @@@Name: list
      @@@Type: Perl|Array
      @@@enDesc: 
        Items to add.
    @@Return:

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: POP
      @@@ContentType: lang|Perl
    @@enDesc:
      Removes the last item of the tied array associated with the object
      and returns the item.
    @@Return:
      @@@Type: DOMMain|any
      @@@enDesc: The removed item.

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: SHIFT
      @@@ContentType: lang|Perl
    @@enDesc:
      Removes the first item of the tied array associated with the object
      and returns the item.
    @@Return:
      @@@Type: DOMMain|any
      @@@enDesc: The removed item.

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: UNSHIFT
      @@@ContentType: lang|Perl
    @@enDesc:
      Inserts items at the beginning of the tied array associated with
      the object.
    @@Param:
      @@@Name: list
      @@@Type: Perl|Array
      @@@enDesc: Items to add.
    @@Return:

  @Method:
    @@ManakaiDOM:isForInternal:1
    @@Operator:
      @@@@: SPLICE
      @@@ContentType: lang|Perl
    @@enDesc:
      Performes the equivalent operation of <Perl::splice> on the
      tied array associated with the object.
    @@Param:
      @@@Name: offset
      @@@Type: DOMMain|long
      @@@enDesc:
        The offset.  The default is <Perl::0>.
    @@Param:
      @@@Name: length
      @@@Type: DOMMain|long
      @@@enDesc:
       The length.  The default is the rest of the array.
    @@Param:
      @@@Name: list
      @@@Type: Perl|Array
      @@@enDesc:
        The item to replace by.
    @@Return:
      @@@Type: Perl|Array
      @@@enDesc:
        A list of the original <P::length> items at <P::offset>.
##TieArray

ElementTypeBinding:
  @Name: IFQName
  @ElementType:
    dis:QName
  @ShadowContent:
    @@ForCheck: ManakaiDOM|ForIF

ElementTypeBinding:
  @Name: ClsQName
  @ElementType:
    dis:QName
  @ShadowContent:
    @@ForCheck: ManakaiDOM|ForClass

ElementTypeBinding:
  @Name: IFISA
  @ElementType:
    dis:ISA
  @ShadowContent:
    @@ForCheck: ManakaiDOM|ForIF

ElementTypeBinding:
  @Name: ClsISA
  @ElementType:
    dis:ISA
  @ShadowContent:
    @@ForCheck: ManakaiDOM|ForClass

ElementTypeBinding:
  @Name: IFPackName
  @ElementType:
    dis:AppName
  @ShadowContent:
    @@ForCheck: ManakaiDOM|ForIF
    @@ContentType: lang|Perl

ElementTypeBinding:
  @Name: ClsPackName
  @ElementType:
    dis:AppName
  @ShadowContent:
    @@ForCheck: ManakaiDOM|ForClass
    @@ContentType: lang|Perl

ElementTypeBinding:
  @Name: IFFileName
  @ElementType:
    dis:FileName
  @ShadowContent:
    @@ForCheck: ManakaiDOM|ForIF

ElementTypeBinding:
  @Name: ClsFileName
  @ElementType:
    dis:FileName
  @ShadowContent:
    @@ForCheck: ManakaiDOM|ForClass

ElementTypeBinding:
  @Name: ICDef
  @ElementType: 
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      @@@@: dis|MultipleResource
      @@@ForCheck: !ManakaiDOM|ForIF !ManakaiDOM|ForClass
    @@resourceFor: ManakaiDOM|ForIF
    @@resourceFor: ManakaiDOM|ForClass

    @@rdf:type:
      @@@@: ManakaiDOM|IF
      @@@ForCheck: ManakaiDOM|ForIF

    @@rdf:type:
      @@@@: ManakaiDOM|Class
      @@@ForCheck: ManakaiDOM|ForClass

    @@Implement:
      @@@@: ||+||ManakaiDOM|ForIF
      @@@ContentType: DISCore|TFPQNames
      @@@ForCheck: ManakaiDOM|ForClass

ElementTypeBinding:
  @Name: CDef
  @ElementType: 
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      @@@@: dis|MultipleResource
      @@@ForCheck: !ManakaiDOM|ForIF !ManakaiDOM|ForClass

    @@resourceFor: ManakaiDOM|ForClass

    @@rdf:type:
      @@@@: ManakaiDOM|Class
      @@@ForCheck: ManakaiDOM|ForClass


ElementTypeBinding:
  @Name: Method
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:Method


ElementTypeBinding:
  @Name: Return
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:MethodReturn

ElementTypeBinding:
  @Name: nullCase
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      ManakaiDOM:InCase
    @@Value:
      @@@is-null:1

ElementTypeBinding:
  @Name: trueCase
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      ManakaiDOM:InCase
    @@Value:true
    @@Type:
      DOMMain:boolean::ManakaiDOM:all

ElementTypeBinding:
  @Name: falseCase
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      ManakaiDOM:InCase
    @@Value:false
    @@Type:
      DOMMain:boolean::ManakaiDOM:all

ElementTypeBinding:
  @Name: enDesc
  @ElementType:
    dis:Description
  @ShadowContent:
    @@lang:en
