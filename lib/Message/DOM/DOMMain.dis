Module:
  @QName:
    MDOM:DOMMain
  @FullName:
    @@lang: en
    @@@:
      DOM Basic Types

  @Namespace:
    http://suika.fam.cx/~wakaba/archive/2004/dom/main#
  
  @Description:
    @@lang:en
    @@@:
      This module provides DOM basic types such as <TYPE::DOMString>
      and classes and properties to describe common DOM concept
      such as <TYPE::ManakaiDOM:ManakaiDOMURI> and
      <TFQ::reflectGet::ManakaiDOM:all>.  In addition,
      it provides the <Class::DOMImplementationRegistry>
      object from which DOM applications can obtain
      various DOM implementations.

  @DISCore:author: DISCore|Wakaba
  @License: license|Perl+MPL
  @Date:
    $Date: 2006/11/05 09:16:29 $
  
  @Require:
    @@Module:
      @@@QName:
        DISlib:DISIDL
      @@@WithFor:
        ManakaiDOM:all
    @@Module:
      @@@WithFor:
        ManakaiDOM:ManakaiDOM
    @@Module:
      @@@WithFor:
        ManakaiDOM:ManakaiDOM1
    @@Module:
      @@@WithFor:
        ManakaiDOM:ManakaiDOM2
    @@Module:
      @@@WithFor:
        ManakaiDOM:ManakaiDOM3
    @@Module:
      @@@WithFor:
        ManakaiDOM:ManakaiDOMLatest
    @@Module:
      @@@QName: MDOM|DOMFeature
      @@@WithFor:
        ManakaiDOM:ManakaiDOMLatest
    @@Module:
      @@@QName: MDOM|DOMCore
      @@@WithFor: ManakaiDOM|ManakaiDOMLatest
  @DefaultFor: ManakaiDOM|ManakaiDOMLatest

Namespace:
  @c:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/dom-core#
  @dis:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#dis--
  @DISlib:
    http://suika.fam.cx/~wakaba/archive/2004/dis/
  @dx:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/Error/DOMException#
  @ecore:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/Error/Core/
  @Err:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/Error/
  @f:
    http://suika.fam.cx/~wakaba/archive/2004/dom/feature#
  @html5:
    http://www.w3.org/1999/xhtml
  @idl:
    http://suika.fam.cx/~wakaba/archive/2004/dis/IDL#
  @infoset:
    http://www.w3.org/2001/04/infoset#
  @j:
    http://suika.fam.cx/~wakaba/archive/2004/dis/Java#
  @jlang:
    java:java.lang.
  @js:
    http://suika.fam.cx/~wakaba/archive/2004/dis/ECMAScript#
  @kwd:
    http://suika.fam.cx/~wakaba/archive/2005/rfc2119/
  @lang:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#
  @license:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/license#
  @ManakaiDOM:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/manakai-dom#
  @MDOM:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/manakai-dom#ManakaiDOM.
  @MDOMX:
    http://suika.fam.cx/~wakaba/archive/2004/8/4/manakai-dom-exception#
  @p:
    http://suika.fam.cx/~wakaba/archive/2004/dis/Perl#
  @rdf:
    http://www.w3.org/1999/02/22-rdf-syntax-ns#
  @rdfs:
    http://www.w3.org/2000/01/rdf-schema#
  @Util:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/
  @UtilError:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/Error/
  @xml:
    http://www.w3.org/XML/1998/namespace
  @xmlns:
    http://www.w3.org/2000/xmlns/

ResourceDef:
  @ForCheck:
    !=ManakaiDOM|all
    ManakaiDOM|ManakaiDOM !=ManakaiDOM|ManakaiDOM !ManakaiDOM|ManakaiDOM3
  @QName:
    MDOM:
  @rdf:type:
    dis:ModuleGroup
  @FullName:
    @@lang:en
    @@@:
      The manakai DOM modules
  @p:packageName:
    @@@:
      Message::DOM::
    @@For: ManakaiDOM|ManakaiDOM
    @@ManakaiDOM:moduleSuffix:
      @@@@: Level2
      @@@For: ManakaiDOM|ManakaiDOM2 !ManakaiDOM|ManakaiDOM3
    @@ManakaiDOM:moduleSuffix:
      @@@@: Level1
      @@@For: ManakaiDOM|ManakaiDOM1 !ManakaiDOM|ManakaiDOM2
  @p:interfacePackageName:
    @@@:
      Message::DOM::IFLevel2::
    @@For: ManakaiDOM|ManakaiDOM2 !ManakaiDOM|ManakaiDOM3
  @p:interfacePackageName:
    @@@:
      Message::DOM::IFLevel1::
    @@For: ManakaiDOM|ManakaiDOM1 !ManakaiDOM|ManakaiDOM2

ElementTypeBinding:
  @Name: ResourceTypeDef
  @ElementType: 
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdfs:Class
    @@rdfs:subClassOf:
      rdfs:Class::ManakaiDOM:all
    @@subsetOf:
      rdfs:Class::ManakaiDOM:all
    @@For: =ManakaiDOM|all

ElementTypeBinding:
  @Name: PropDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdf:Property
    @@For: =ManakaiDOM|all

ElementTypeBinding:
  @Name: Attr
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISLang|Attribute
    @@ForCheck:
      ManakaiDOM:DOM !=ManakaiDOM|ManakaiDOM

ElementTypeBinding:
  @Name: Get
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISLang|AttributeGet

ElementTypeBinding:
  @Name: Set
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISLang|AttributeSet

ResourceTypeDef:
  @QName: ReflectGet
  @enDesc:
    Getter templates for DOM attributes <QUOTE::reflecting> 
    element attributes.
  @rdfs:subClassOf:
    DISCore:AnyDefContainer

ResourceTypeDef:
  @QName: ReflectSet
  @enDesc:
    Setter templates for DOM attributes <QUOTE::reflecting>
    element attributes.
  @rdfs:subClassOf:
    DISCore:AnyDefContainer

PropDef:
  @QName: reflectGet
  @enDesc:
    Getter template for a <QUOTE::reflecting> DOM attribute.
  @dataType: dis|TFQNames
  @rdfs:range: ReflectGet

PropDef:
  @QName: reflectSet
  @enDesc:
    Setter template for a <QUOTE::reflecting> DOM attribute.
  @dataType: dis|TFQNames
  @rdfs:range: ReflectSet

ElementTypeBinding:
  @Name: Method
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISLang|Method
    @@ForCheck: !=ManakaiDOM|ManakaiDOM

ElementTypeBinding:
  @Name: Param
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISLang|MethodParameter

ElementTypeBinding:
  @Name: FormatDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISCore|LexicalType
    @@For: =ManakaiDOM|all
    @@rdfs:subClassOf:
      DISCore:String::ManakaiDOM:all

ElementTypeBinding:
  @Name: Return
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISLang|MethodReturn

ElementTypeBinding:
  @Name: InCase
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      ManakaiDOM:InCase

## -- DOM specific properties and types

PropDef:
  @QName:
    dis:NSVersion
  @Description:
    @@lang:en
    @@@:
      A namespace-aware variant of the method or attribute.

PropDef:
  @QName:
    dis:NoNSVersion
  @Description:
    @@lang:en
    @@@: A namespace-unaware variant of the method or attribute.

PropDef:
  @Name: isNamespaceAware
  @Description:
    @@lang:en
    @@@: 
      Whether a subject resource is namespace-aware or not.  Note that 
      the property value of <CODE::false> does not mean the subject 
      resource is namespace unaware; it might be namespace-neutral.
  @dataType:
    dis:Boolean

PropDef:
  @Name: isNamespaceUnaware
  @Description:
    @@lang:en
    @@@:
      Whether a subject resource is namespace-unaware or not.

## -- Obsolete properties

## NOTE: These definitions will be removed after 
##       all these instances removed from the <QUOTE::dis> source files.

PropDef:
  @QName:
    dis:Redefine
  @Description:
    @@lang:en
    @@@: Declares redefining (obsolete)
PropDef:
  @QName:
    dis:RedefinedBy
  @Description:
    @@lang:en
    @@@: Declares redefined in elsewhere (obsolete)
PropDef:
  @QName:
    dis:Redefined
  @Description:
    @@lang:en
    @@@: Declares redefined in elsewhere (obsolete)

## -- Sugar

ElementTypeBinding:
  @Name: PerlDef
  @ElementType:
    dis:Def
  @ShadowContent:
    @@ContentType:
      lang:Perl

## -- DOM Core Specification datatypes

ResourceDef:
  @For:
    ManakaiDOM:DOM2
  @QName: DOMTimeStamp
  @enLabel:
    <DOM::DOMTimeStamp>

  @DISCore:resourceType: DISLang|DataType
  @Description:
    @@lang:en
    @@@: 
      An absolute or relative time.  It is represented 
      as a number of milliseconds.

  @rdfs:subClassOf: idl|unsignedLongLong||ManakaiDOM|all

  @idl:typedefAs: idl|unsignedLongLong
  @idl:java: j|long
  @idl:perl: p|Number
  @idl:ecmascript: js|Date
##DOMTimeStamp

ResourceDef:
  @DISCore:resourceType: DISLang|DataType
  @QName: DOMUserData
  @For: ManakaiDOM|DOM3
  @enLabel:
    <DOM::DOMUserData>
  
  @enDesc: A reference to application data.

  @Def:
    @@ContentType: lang|dis
    @@DISLang:dataTypeAliasFor: DOMMain|any||ManakaiDOM|all
    @@For: ManakaiDOM|IDL

  @idl:java: jlang|Object
  @idl:ecmascript: js|AnyType
  @idl:perl: p|Any
##DOMUserData

ElementTypeBinding:
  @Name: enDesc
  @ElementType:
    dis:Description
  @ShadowContent:
    @@lang:en

ResourceDef:
  @DISCore:resourceType: DISLang|DataType
  @QName: DOMObject
  @For: ManakaiDOM|DOM3
  @enLabel:
    <DOM::DOMObject>

  @rdfs:subClassOf: idl|Object||ManakaiDOM|all
  
  @enDesc: An object reference.

  @idl:typedefAs: idl|Object  
  @idl:java: j|Object
  @idl:ecmascript:
    @@@: js|Object
    @@ImplNote:
      @@@lang:en
      @@@@:
        DOM spec text describes as the <TYPE::DOMObject> is bound
        to <TYPE::js|ObjectType> type while DOM ECMAScript binding
        appendix describes as it is an object which
        implements the <Class::js|Object> interface.
  @idl:perl: p|Object
##DOMObject

ResourceDef:
  @QName: DOMString
  @DISCore:resourceType: DISLang|DataType
  @enLabel:
    <DOM::DOMString>

  @idl:sequenceOf: idl|unsignedShort
  @idl:java: jlang|String
  @idl:ecmascript: js|StringType
  @idl:perl: p|CharacterString

  @rdfs:subClassOf: DISLang|String||ManakaiDOM|all

  @Description:
    @@lang:en
    @@@: 
      A string.  It is used to store Unicode characters.
##DOMString

## -- DOM specialized string types

ElementTypeBinding:
  @Name: StringDataTypeDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISLang|DataType
    @@rdfs:subClassOf: DOMString
    @@ForCheck:
      ManakaiDOM:ManakaiDOM !=ManakaiDOM:ManakaiDOM

StringDataTypeDef:
  @QName: 
    ManakaiDOM:ManakaiDOMURI
  @FullName: 
    @@lang:en
    @@@: DOM URI
  @Description:
    @@lang:en
    @@@:
      {P:: DOM URI.  It is a specialized case of the <TYPE::DOMString> and 
      that met the conditions:
    \
      = An absolute identifier absolutely identifies a resource on the Web.
    \
      = Simple string equality of absolute resource identifiers. 
      No other equaivalence is considered significant to the 
      DOM specification.
    \
      = A relative identifier is easily detected and made absolute 
      relative to an absolute identifier.
    \
      = Retrieval of content of a resource may be accomplished 
      where required.
      
      }
      
      {NOTE:: Examples of DOM URI would include RFC 3986 URI and
              RFC 3987 IRI.
      }

  @ResourceDef:
    @@QName: isRelativeDOMURI
    @@rdf:type: p|InlineCode
    @@Description:
      @@@lang:en
      @@@@:
        Whether a string is a relative DOM URI or not. 
    @@PerlDef:
      @@@@:
        ($INPUT !~ m!\A[^:/?#]+:!)
      @@@ImplNote:
        @@@@lang:en
        @@@@@:
          {ISSUE::
            Where a relative reference resolution is required,
            we don't do it unless the reference has no scheme part.
            However, it leads dot-segments in the reference
            not cleaned up.  Is it good or bad?
          }

StringDataTypeDef:
  @QName: DOMMain|DOMURIs
  @FullName:
    @@lang:en
    @@@: white space separeted list of DOM URIs
  @enDesc:
    A <TYPE::DOMMain:DOMURIs> is a <IF::DOMMain:DOMString>
    containing a list of DOM URIs separated by white space
    characters (<CODE::S> in XML 1.0).
  @ResourceDef:
    @@rdf:type: DISLang|InputProcessor
    @@PerlDef:
      if (defined $INPUT and not ref $INPUT) {
        $INPUT = [split /[\x09\x0A\x0D\x20]+/, $INPUT];
      }
  @ResourceDef:
    @@QName: DOMMain|stringifyDOMURIs
    @@enDesc:
      Generates a string representation of DOM URIs.
    @@rdf:type: p|InlineCode
    @@PerlDef:
      (join ' ', @{$uris})
##DOMURIs

StringDataTypeDef:
  @QName: RFC2732URIReference
  @Label:
    @@lang:en
    @@@:
      RFC 2396 / 2732 URI reference
  @FullName:
    @@lang:en
    @@@:
      URI reference as defined in RFC 2396 amended by RFC 2732
  @rdfs:subClassOf:
    ManakaiDOM:ManakaiDOMURI

StringDataTypeDef:
  @QName: RFC2396URIReference
  @Label:
    @@lang:en
    @@@:
      RFC 2396 URI reference
  @FullName:
    @@lang:en
    @@@:
      URI reference as defined in RFC 2396
  @rdfs:subClassOf: RFC2732URIReference

StringDataTypeDef:
  @QName: RFC2396URI
  @Label:
    @@lang:en
    @@@:
      RFC 2396 URI
  @FullName:
    @@lang:en
    @@@:
      URI as defined in RFC 2396
  @rdfs:subClassOf: RFC2396URIReference

StringDataTypeDef:
  @QName: RFC2396AbsoluteURI
  @Label:
    @@lang:en
    @@@:
      RFC 2396 absolute URI
  @FullName:
    @@lang:en
     @@@:
       Absolute URI as defined in RFC 2396
  @rdfs:subClassOf: RFC2396URI

## TODO: Output escape

StringDataTypeDef:
  @QName: 
    ManakaiDOM:ManakaiDOMNamespaceURI
  @FullName:
    @@lang:en
    @@@: DOM Namespace URI
  @rdfs:subClassOf:
    ManakaiDOM:ManakaiDOMURI
  @Description:
    @@lang:en
    @@@:
      A namespace URI.  In general, DOM implementations does not 
      perform any URI normalization or canonicalization.  The URIs 
      given to the DOM are assumed to be valid and no lexical 
      checking is performed.
      
      Absolute URI references are treated 
      as strings and compared literally.  How relative namespace URI 
      references are treated is undefined.  DOM applications should 
      use <DOM::null> if they wish to have no namespace.
      
      Some programing languages can differentiate empty strings 
      from <DOM::null>.  Empty strings were specified as a legal namespace 
      URI in the DOM Level 2 Specification but the DOM Level 2 Errata 
      refines this as the implementaton dependent matter.  The 
      DOM Level 3 Specification again refines to be converted to <DOM::null>.
  @ImplNote:
    @@lang:en
    @@@: 
      The <FILE::dis2pm.pl> is coded so that if the input (the parameter of 
      a method or the given value of an attribute) is typed as 
      <TYPE::ManakaiDOMNamespaceURI>, the Perl code to check the empty 
      string is outputed as part of the implementation.

  @ResourceDef:
    @@rdf:type: DISLang|InputProcessor
    @@PerlDef:
      if (CORE::defined $INPUT and CORE::length $INPUT == 0) {
        require Message::DOM::DOMMain;
        __DOMCore:ERROR{DOMMain|empty-namespace-uri::
        }__;

        __FOR{ManakaiDOM:DOM3::
          $INPUT = undef;
        }__;
      }

StringDataTypeDef:
  @QName: ManakaiDOMSchemaType
  @FullName:
    @@lang:en
    @@@:
      Schema language type
  @Description:
    @@lang:en
    @@@:
      An absolute URI representing the type of the schema language 
      <SRC::DOM Level 3 Core <CP::schema-type>, DOM Level 3 Load and Save
            <M::DOMLS:DOMImplementationLS.createLSParser>>.
      \
      {NOTE:: Schema languages other than XML Schema and XML DTD 
              are outside of the scope of W3C; they should 
              recommend their identifying absolute URI 
              <SRC::DOM Level 3 Core <CP::schema-type>, 
                    DOM Level 3 Load and Save 
                    <M::DOMLS:DOMImplementationLS.createLSParser>>.
      \
      }
  @rdfs:subClassOf:
    ManakaiDOM:ManakaiDOMURI
  @InCase:
    @@Value:
      @@@@:
        http://www.w3.org/2001/XMLSchema
      @@@ContentType: DISCore|URI
    @@Description:
      @@@lang:en
      @@@@:
        The XML Schema definition language 
        <SRC::DOM Level 3 Core <CP::schema-type>, 
              DOM Level 3 Load and Save
              <M::DOMLS:DOMImplementationLS.createLSParser>>.
  @InCase:
    @@Value:
      @@@@:
        http://www.w3.org/TR/REC-xml
      @@@ContentType: DISCore|URI
    @@Description:
      @@@lang:en
      @@@@:
        The XML 1.0 Document Type Definition 
        <SRC::DOM Level 3 Core <CP::schema-type>, 
              DOM Level 3 Load and Save
        <M::DOMLS:DOMImplementationLS.createLSParser>>.

ImplNote:
  @lang:en
  @@:
    {TODO::
      Defines schema-type features
      (See <URI::http://suika.fam.cx/www/2006/schema/>).
    }


    TODO: Move key identifier(s) to keyboard event module.

StringDataTypeDef:
  @QName:
    ManakaiDOM:ManakaiDOMKeyIdentifier
  @For:
    ManakaiDOM:ManakaiDOM3
  @FullName:
    @@lang:en
    @@@: DOM key identifier
  @Description:
    @@lang:en
    @@@:
      A key identifier.
    \
      {NOTE:: A set of key identifiers are defined in the Appendix A 
              of the <CITE::DOM Level 3 Events> Specification.
    \
      }

StringDataTypeDef:
  @QName:
    ManakaiDOM:ManakaiDOMKeyIdentifiers
  @For:
    ManakaiDOM:ManakaiDOM3
  @FullName:
    @@lang:en
    @@@: List of DOM key identifiers
  @Description:
    @@lang:en
    @@@:
      Zero or more <TYPE::ManakaiDOM:ManakaiDOMKeyIdentifier> separated 
      by white space (XML 1.0 <CODE::S>). 
      \
      In the manakai DOM implementations, method parameters and 
      attributes whose actual datatype is 
      <TYPE::ManakaiDOM:ManakaiDOMKeyIdentifiers> also accept a reference 
      to an array whose items are key identifier strings as well as 
      a plain string of identifiers.

  @ResourceDef:
    @@rdf:type: DISLang|InputProcessor
    @@PerlDef:
      if (CORE::defined $INPUT) {
        unless (CORE::ref $INPUT) {
          $INPUT = [CORE::split /[\x20\x09\x0D\x0A]/, $INPUT];
        }
      } else {
        $INPUT = [];
      }

StringDataTypeDef:
  @QName: ManakaiDOMXMLName
  @FullName:
    @@lang:en
    @@@: XML Name
  @ResourceDef:
    @@rdf:type: DISLang|InputProcessor
    @@PerlDef: \

  @ResourceDef:
    @@QName: testXML10Name
    @@enDesc:
      Tests whether a scalar variable string is a valid XML 1.0 
      <CODE::Name> or not.
    @@PerlDef:
      ($INPUT =~ /\A\p{inxml:_NameStartChar10}\p{inxml:NameChar10}*\z/)
    @@rdf:type: p|InlineCode
  
  @ResourceDef:
    @@QName: testXML11Name
    @@enDesc: 
      Tests whether a scalar variable string is a valid XML 1.1
      <CODE::Name> or not.
    @@PerlDef:
      ($INPUT =~ /\A\p{inxml:NameStartChar11}\p{inxml:NameChar11}*\z/)
    @@rdf:type: p|InlineCode

  @ResourceDef:
    @@QName: ensureXMLName
    @@rdf:type: p|BlockCode
    @@Description:
      @@@lang:en
      @@@@: 
        Tests whether a string is a legal XML Name and 
        if not, raises an exception.
        \
        {NOTE:: This code fragment does not support non-<Feature::XML>. 
        \
        }
    @@ImplNote:
      @@@lang:en
      @@@@:
        What should be done for HTML name is not studied yet. 
    @@PerlDef:
        if (defined $INPUT) {
          __FOR{ManakaiDOM:DOM3::
            if (((not defined $XMLVERSION or $XMLVERSION eq '1.0') and
                 not <Code::DOMMain:testXML10Name:: $INPUT = $INPUT>) or
                (defined $XMLVERSION and $XMLVERSION eq '1.1' and
                 not <Code::DOMMain:testXML11Name:: $INPUT = $INPUT>)) {
              __EXCEPTION{MDOMX:MDOM_BAD_NAME::
                DOMCore:name => {$INPUT},
                MDOMX:param-name => '$INPUT_PARAM_NAME',
                infoset:version => {$XMLVERSION},
              }__;
            }
          }__;
          __FOR{!ManakaiDOM:DOM3::
            unless (<Code::DOMMain:testXML10Name:: $INPUT = $INPUT>) {
              __EXCEPTION{MDOMX:MDOM_BAD_NAME::
                DOMCore:name => {$INPUT},
                MDOMX:param-name => '$INPUT_PARAM_NAME',
                infoset:version => '1.0',
              }__;
            }
          }__;
        }

StringDataTypeDef:
  @QName: ManakaiDOMXMLNCName
  @rdfs:subClassOf: ManakaiDOMXMLName
  @FullName:
    @@lang:en
    @@@: XML NCName

  @ResourceDef:
    @@rdf:type: DISLang|InputProcessor
    @@PerlDef:\

  @ResourceDef:
    @@QName: testXML10NCName
    @@rdf:type: p|InlineCode
    @@enDesc:
      Tests whether a scalar variable string is a valid XML Namespace
      1.0 <CODE::NCName> or not.
    @@PerlDef:
      ($INPUT =~ /\A\p{inxml:_NCNameStartChar10}\p{inxml:NCNameChar10}*\z/)

  @ResourceDef:
    @@QName: testXML11NCName
    @@rdf:type: p|InlineCode
    @@enDesc:
      Tests whether a scalar variable string is a valid XML Namespace
      1.1 <CODE::NCName> or not.
    @@PerlDef:
      ($INPUT =~ /\A\p{inxml:NCNameStartChar11}\p{inxml:NCNameChar11}*\z/)

StringDataTypeDef:
  @QName: ManakaiDOMNamespacePrefix
  @rdfs:subClassOf: ManakaiDOMXMLNCName
  @FullName:
    @@lang:en
    @@@: XML Namespace Prefix

  @ResourceDef:
    @@QName: ensureNamespacePrefix
    @@rdf:type: p|BlockCode
    @@Description:
      @@@lang:en
      @@@@: 
        Tests whether a string is a legal XML Namespace prefix and 
        if not, raises an exception.
        \
        {NOTE:: This code fragment does not support non-<Feature::XML>. 
        \
        }
    @@Description:
      @@@lang:en
      @@@@:
        The DOM Level 3 Core Specification specifies that how
        the empty prefix string is treated is implementation-dependent;
        the manakai DOM implementations does not distingulish
        it with the <DOM::null> value.
      @@@For:
        ManakaiDOM:DOM3
    @@PerlDef:
      if (defined $INPUT and $INPUT eq '') {
        require Message::DOM::DOMMain;
        __c|ERROR{DOMMain|empty-namespace-prefix::
        }__;
        undef $INPUT;
      }
      if (defined $INPUT) {
        if (($XMLVERSION eq '1.0' and
             not <Code::DOMMain|testXML10Name:: $INPUT = $INPUT>) or
            ($XMLVERSION eq '1.1' and
             not <Code::DOMMain|testXML11Name:: $INPUT = $INPUT>)) {
          __EXCEPTION{MDOMX|MDOM_BAD_NAME::
            infoset|name => {$INPUT},
            MDOMX|param-name => '$INPUT_PARAM_NAME',
            infoset|version => {$XMLVERSION},
          }__;
        } elsif (index ($INPUT, ':') > -1) {
          __EXCEPTION{MDOMX|MDOM_NS_BAD_NCNAME::
            infoset|name => {$INPUT},
            MDOMX|param-name => '$INPUT_PARAM_NAME',
            infoset|version => {$XMLVERSION},
          }__;
        }
      } # has prefix

ErrDef:
  @QName: DOMMain|empty-namespace-uri
  @enDesc:
    An empty string is used as a namespace URI.
  @DOMCore:severity: DOMCore|SEVERITY_WARNING

ErrDef:
  @QName: DOMMain|empty-namespace-prefix
  @enDesc:
    An empty string is used as a namespace prefix.
  @DOMCore:severity: DOMCore|SEVERITY_WARNING

ElementTypeBinding:
  @Name: ErrDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DOMCore|DOMErrorType
    @@For: ManakaiDOM|DOM3
    @@ecore:textFormatter: ecore|MUErrorFormatter||ManakaiDOM|Perl

StringDataTypeDef:
  @QName: ManakaiDOMXMLLocalName
  @rdfs:subClassOf: ManakaiDOMXMLNCName
  @FullName:
    @@lang:en
    @@@: XML local name

StringDataTypeDef:
  @Name: ManakaiDOMXMLQName
  @rdfs:subClassOf: ManakaiDOMXMLName
  @enLabel:
    XML QName
  @FullName:
    @@lang:en
    @@@:
      XML Qualified Name

  @ResourceDef:
    @@QName: ensureXMLQName
    @@rdf:type: p|BlockCode
    @@Description:
      @@@lang:en
      @@@@:
        Checks whether a string is a legal XML qualified name and if not 
        then raises an exception.
    @@PerlDef:
      if (defined $XMLVERSION and $XMLVERSION eq '1.1') {
          unless (<Code::DOMMain:testXML11Name:: $INPUT = $INPUT>) {
            __EXCEPTION{
              MDOMX:MDOM_BAD_NAME::
                DOMCore:name => {$INPUT},
                MDOMX:param-name => '$INPUT_NAME',
                infoset:version => '1.1',
            }__;
          }
          unless (<Code::DOMMain:testXML11QName:: $INPUT = $INPUT>) {
            __EXCEPTION{
              MDOMX:MDOM_NS_MALFORMED_QNAME::
                DOMCore:name => {$INPUT},
                MDOMX:param-name => '$INPUT_NAME',
                infoset:version => '1.1',
            }__;
          }
        } else {
          unless (<Code::DOMMain:testXML10Name:: $INPUT = $INPUT>) {
            __EXCEPTION{
              MDOMX:MDOM_BAD_NAME::
                DOMCore:name => {$INPUT},
                MDOMX:param-name => '$INPUT_NAME',
                infoset|version => {$XMLVERSION},
            }__;
          }
          unless (<Code::DOMMain:testXML10QName:: $INPUT = $INPUT>) {
            __EXCEPTION{
              MDOMX:MDOM_NS_MALFORMED_QNAME::
                DOMCore:name => {$INPUT},
                MDOMX:param-name => '$INPUT_NAME',
                infoset|version => {$XMLVERSION},
            }__;
          }
        }
StringDataTypeDef:
  @QName: ManakaiDOMXML10QName
  @rdfs:subClassOf: ManakaiDOMXML10QName
  @enDesc:
    XML 1.0 Namespace Qualified Names.
  @enLabel:
    XML 1.0 QName
  
  @ResourceDef:
    @@rdf:type: DISLang|InputProcessor
    @@PerlDef:
    if (<Code::DOMCore:doStrictErrorChecking:: $nodeRef = $self>) {
      unless (<Code::DOMMain:testXML10Name:: $INPUT = $INPUT>) {
        __EXCEPTION{MDOMX:MDOM_BAD_NAME::
            DOMCore:name => {$INPUT},
            MDOMX:param-name => '$INPUT_NAME',
            infoset:version => '1.0',
        }__;
      }
      unless (<Code::DOMMain:testXML10QName:: $INPUT = $INPUT>) {
        __EXCEPTION{MDOMX:MDOM_NS_MALFORMED_QNAME::
          DOMCore:name => {$INPUT},
          MDOMX:param-name => '$INPUT_NAME',
          infoset:version => '1.0',
        }__;
      }
    }

  @ResourceDef:
    @@QName: testXML10QName
    @@rdf:type: p|InlineCode
    @@enDesc:
      Tests whether a scalar variable string is a valid XML
      Namespace 1.0 qualified name or not.
    @@PerlDef:
      ($INPUT =~ /\A\p{inxml:_NCNameStartChar10}\p{inxml:NCNameChar10
                  }*(?::\p{inxml:_NCNameStartChar10}\p{inxml:NCNameChar10}*)?\z/)

ElementTypeBinding:
  @Name: enLabel
  @ElementType:
    dis:Label
  @ShadowContent:
    @@lang:en

StringDataTypeDef:
  @QName: ManakaiDOMXML11QName
  @rdfs:subClassOf: ManakaiDOMXMLQName
  @enDesc:
    XML 1.1 Namespace Qualified Name.
  @enLabel:
    XML 1.1 QName
  
  @ResourceDef:
    @@rdf:type: DISLang|InputProcessor
    @@PerlDef:
    if (<Code::DOMCore:doStrictErrorChecking:: $nodeRef = $self>) {
      unless (<Code::DOMMain:testXML11Name:: $INPUT = $INPUT>) {
        __EXCEPTION{MDOMX:MDOM_BAD_NAME::
          DOMCore:name => {$INPUT},
          MDOMX:param-name => '$INPUT_NAME',
          infoset:version => '1.1',
        }__;
      }
      unless (<Code::DOMMain:testXML11QName:: $INPUT = $INPUT>) {
        __EXCEPTION{MDOMX:MDOM_NS_MALFORMED_QNAME::
          DOMCore:name => {$INPUT},
          MDOMX:param-name => '$INPUT_NAME',
          infoset:version => '1.1',
        }__;
      }
    }

  @ResourceDef:
    @@QName: testXML11QName
    @@rdf:type: p|InlineCode
    @@enDesc:
      Tests whether a scalar variable string is a valid XML
      Namespace 1.1 qualified name or not.
    @@PerlDef:
      ($INPUT =~ /\A\p{inxml:NCNameStartChar11}\p{inxml:NCNameChar11
                  }*(?::\p{inxml:NCNameStartChar11}\p{inxml:NCNameChar11}*)?\z/)
        

ResourceDef:
  @QName: checkXMLNamesQName
  @rdf:type: p|BlockCode
  @Description:
    @@lang:en
    @@@:
      Checks validness of XML Namespace Name (Namespace URI) and QName. 
      \
      {NOTE:: This code fragment checks their combination; it does not 
              test whether the URI or the QName is itself valid or not.
      \
      }

    For DOM Level 3 Core <A::tc|Element.setAttributeNS>.
  @PerlDef:
    if (defined $PREFIX) {
      if (not defined $NSURI) {
        __EXCEPTION{MDOMX|MDOM_NS_PREFIX_WITH_NULL_URI::
          infoset|prefix => {$PREFIX},
        }__;
      } elsif ($PREFIX eq 'xml' and $NSURI ne <Q::xml:>) {
        __EXCEPTION{MDOMX:MDOM_NS_XML_WITH_OTHER_URI::
          infoset:namespaceName => {$NSURI},
        }__;
      } elsif ($PREFIX eq 'xmlns' and $NSURI ne <Q::xmlns:>) {
        __EXCEPTION{MDOMX:MDOM_NS_XMLNS_WITH_OTHER_URI::
          infoset:namespaceName => {$NSURI},
        }__;
      } elsif ($NSURI eq <Q::xml:> and $PREFIX ne 'xml') {
        __EXCEPTION{MDOMX:MDOM_NS_OTHER_WITH_XML_URI::
          infoset:prefix => {$PREFIX},
        }__;
      } elsif ($NSURI eq <Q::xmlns:> and $PREFIX ne 'xmlns') {
        __EXCEPTION{MDOMX:MDOM_NS_OTHER_WITH_XMLNS_URI::
          infoset:prefix => {$PREFIX},
        }__;
      }
    } else { ## No prefix
      if (not defined $NSURI) {
        #
      } elsif ($NSURI eq <Q::xmlns:> and $LNAME ne 'xmlns') {
        __EXCEPTION{MDOMX:MDOM_NS_OTHER_WITH_XMLNS_URI::
          infoset:prefix => {undef},
        }__;
      }
    }

## -- Bootstrap DOMImplementationRegistry object

ResourceDef:
  @QName: 
    DOMMain:ManakaiDOMImplementationRegistryVar
  @rdf:type:
    p:ScalarVariable
  @p:variableName:
    Message::DOM::DOMImplementationRegistry
  @DefaultValue:
    @@@:
      <ClassName::DOMImplementationRegistry::ManakaiDOM:ManakaiDOMLatest>
    @@ContentType:
      lang:Perl
  @Type: DISLang|String||ManakaiDOM|all
  @For:
    ManakaiDOM:ManakaiDOMLatest

ResourceDef:
  @QName: DOMImplementationRegistry
  @rdf:type: ManakaiDOM|Class

  @For: ManakaiDOM|DOM3

  @Implement: DOMCore|DOMImplementationSource||ManakaiDOM|ManakaiDOM3
  @Implement:
    @@@: DOMCore|DOMImplementationSource||ManakaiDOM|ManakaiDOMLatest
    @@For: ManakaiDOM|ManakaiDOMLatest

  @ISA: DOMFeature|ImplementationRegistry

  @enDesc:
    The <Class::DOMImplementationRegistry> object provides 
    two methods to find DOM implementations.

  @Method:
     @@Name:  getDOMImplementation
     @@ManakaiDOM:isStatic: 1
     @@Description:
       @@@lang:en
       @@@@:
         Seeks and returns the first implementation that implements 
         desired features. 
     @@Param:
        @@@Name:  features
        @@@Type:  DOMMain|DOMString||ManakaiDOM|all
        @@@actualType: DOMFeature|FeaturesString
        @@@Description:
          @@@@lang:en
          @@@@@: 
            A list of desired features that must be implemented.
     @@Return:
       @@@Type: DOMCore|DOMImplementation
       @@@enDesc:
         The first DOM implementation that has the desired features.
         The object returned by this method <kwd:MUST> implement
         the <IF::DOMCore:DOMImplementation> interface defined
         in DOM Level 3 and it <kwd:MUST> has the feature
         <Feature::Core> version <FeatureVer::3.0>.
       @@@enDesc:
         @@@@For: ManakaiDOM|ManakaiDOM
         @@@@@:
           {NOTE:: The object returned by this method might not
                   be a manakai object.
           }
       @@@InCase:
         @@@@Value:
           @@@@@is-null:1
         @@@@Description:
           @@@@@lang:en
           @@@@@@: 
             No implementation implementing the desired features is found.
      @@@Def:
        @@@@ContentType:
          lang:dis
        @@@@p:cloneCode:
          DOMFeature|ImplementationRegistry.getDOMImplementation

  @Method:
     @@Name:  getDOMImplementationList
     @@ManakaiDOM:isStatic: 1
     @@Description:
       @@@lang:en
       @@@@:
         Returns a list of all the DOM implementations that implement 
         desired features.
     @@Param:
        @@@Name:  features
        @@@Type:  DISLang|String||ManakaiDOM|all
        @@@actualType: DOMFeature|FeaturesString
        @@@Description:
          @@@@lang:en
          @@@@@: A list of desired features that must be implemented.
     @@Return:
       @@@Type:  DOMCore|DOMImplementationList
       @@@actualType: DOMCore|ManakaiDOMImplementationList
       @@@Description:
         @@@@lang:en
         @@@@@: 
           A list of DOM implementations that support the desired features. 
           The object returned by this method <kwd:MUST> implement
           the <IF::DOMCore:DOMImplementation> interface defined
           in DOM Level 3 and it <kwd:MUST> has the feature
           <Feature::Core> version <FeatureVer::3.0>.

           {NOTE:: Since implementation sources reports the same
                   implementation, the list <kwd:MAY> contain an implementation
                   more than one times.
           }
       @@@enDesc:
         @@@@For: ManakaiDOM|ManakaiDOM
         @@@@@:
           {NOTE:: Although the list returned by this method itself
                   is a manakai object, each item in the list might
                   not a manakai object.
           }
       @@@PerlDef:
         __DEEP{       ## NOTE: Method name directly written
           $r = bless $self->SUPER::get_dom_implementation_list ($features),
                      <ClassName::DOMCore|ManakaiDOMImplementationList>;
         }__;
##DOMImplementationRegistry
