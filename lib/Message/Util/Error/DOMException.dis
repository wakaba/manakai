Module:
  @QName:
    Err:DOMException
  @FullName:
    @@lang: en
    @@@:
      Manakai DOM-Style Error Reporting System Implementation
  @Namespace:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/Error/DOMException#
  
  @Description:
    @@lang:en
    @@@:
      This module provides the manakai implementation of DOM-style 
      error reporting system build on the top of the 
      <PerlModule::Message::Util::Error>. 

  @DISCore:author: DISCore|Wakaba
  @License:
     license:Perl+MPL
  @Date:
    @@@:
      $Date: 2006/02/08 08:18:31 $
    @@ContentType:
      dis:Date.RCS
  
  @Require:
    @@Module:
      @@@QName:
        DISlib:DISPerl
      @@@WithFor:
        ManakaiDOM:all
    @@Module:
      @@@QName:
        DISlib:DISIDL
      @@@WithFor:
        ManakaiDOM:all
    @@Module:
      @@@QName: Err|DOMException
      @@@WithFor:
        ManakaiDOM:Perl
    @@Module:
      @@@QName: Err|Core
      @@@WithFor: ManakaiDOM|Perl
  @DefaultFor:
    ManakaiDOM:Perl

Namespace:
  @dis:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#dis--
  @DISlib:
    http://suika.fam.cx/~wakaba/archive/2004/dis/
  @disPerl:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#dis--Perl--
  @DOMCore:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/dom-core#
  @DOMMain:
    http://suika.fam.cx/~wakaba/archive/2004/dom/main#
  @dx:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/Error/DOMException#
  @ecore:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/Error/Core/
  @Err:
    http://suika.fam.cx/~wakaba/archive/2005/manakai/Util/Error/
  @idl:
    http://suika.fam.cx/~wakaba/archive/2004/dis/IDL#
  @kwd:
    http://suika.fam.cx/~wakaba/archive/2005/rfc2119/
  @lang:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/lang#
  @license:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/license#
  @ManakaiDOM:
    http://suika.fam.cx/~wakaba/archive/2004/8/18/manakai-dom#
  @MDOMX:
    http://suika.fam.cx/~wakaba/archive/2004/8/4/manakai-dom-exception#
  @rdf:
    http://www.w3.org/1999/02/22-rdf-syntax-ns#
  @rdfs:
    http://www.w3.org/2000/01/rdf-schema#

## -- Module group for documentation

ResourceDef:
  @For: =ManakaiDOM|all
  @rdf:type: dis|ModuleGroup

  @QName: dx|ManakaiErrorModules
  @Label:
    @@@: manakai Error Modules
    @@lang:en
  @FileName: Message/Util/errors

  @enDesc:
    The <DFN::manakai Error Modules> is a set of modules that
    provide facilities for error and exception handling.

    The <Class::ecore|Error||ManakaiDOM|Perl> module, which is
    strictly not part of manakai, enables Perl programs
    to throw exceptions and to catch them by <Perl::try> and
    <Perl::catch> syntax.

    The <Class::ecore|MUError||ManakaiDOM|Perl> module
    extends the <Class::ecore|Error||ManakaiDOM|Perl> module
    so that programs can report errors by various ways,
    e.g. via <Class::ecore|Error||ManakaiDOM|Perl>'s exception
    <Perl::throw>ing or via so-called error handler code, through
    the one <Perl::report> statement.

    The <Module::Err|DOMException||ManakaiDOM|Perl> module
    again extends the <Class::ecore|MUError||ManakaiDOM|Perl>
    module and it provides a base implementation for
    DOM-compatible exception system.  Not only it has
    an attribute for DOM error code, it also has a
    property for <QUOTE::sub-code> that describes the 
    situation clearer than the DOM code.  In addition,
    parameters can be defined for each sub-codes.

  @DISCore:module: Err|Core||ManakaiDOM|Perl
  @DISCore:module: Err|DOMException||ManakaiDOM|Perl
##ManakaiErrorModules

ElementTypeBinding:
  @Name: enDesc
  @ElementType:
    dis:Description
  @ShadowContent:
    @@lang:en

ElementTypeBinding:
  @Name: enFN
  @ElementType:
    dis:FullName
  @ShadowContent:
    @@lang:en

## -- Resource types

ResourceTypeDef:
  @QName: AnyExceptionAnyClass
  @FullName:
    @@lang:en
    @@@:
      Any <QUOTE::exception> class or interface
  @Description:
    @@lang:en
    @@@:
      Concrete implementations of any type of <QUOTE::exception>,
      including <QUOTE::error> and <QUOTE::warning>.
  @rdfs:subClassOf: DISLang|AnyClass
  @rdfs:subClassOf: Err|MUErrorAny

ResourceTypeDef:
  @QName: dx|Interface
  @enFN:
    DOM-style exception interface
  @enDesc:
    A <Q::dx|Interface> is a <Q::DISLang:Interface> which should 
    be implemented in the manner compatible with DOM exceptions.
    It is expected that the interfaces of this type have
    a <CODE::code> attribute which returns the error code.
  @rdfs:subClassOf:
    AnyExceptionAnyClass::ManakaiDOM:all
  @rdfs:subClassOf: Err|MUErrorInterface
  @rdfs:subClassOf: DISLang|Exception

ResourceTypeDef:
  @QName: AnyExceptionClass
  @FullName:
    @@lang:en
    @@@:
      Any <QUOTE::exception> class
  @Description:
    @@lang:en
    @@@:
      Concrete implementations of any type of <QUOTE::exception>,
      including <QUOTE::error> and <QUOTE::warning>.
  @rdfs:subClassOf:
    AnyExceptionAnyClass::ManakaiDOM:all
  @rdfs:subClassOf: Err|MUErrorClass

ResourceTypeDef:
  @QName: dx|Class
  @enFN: DOM-style exception class
  @Description:
    @@lang:en
    @@@:
      A concrete implementation of an exception type.
  @rdfs:subClassOf:
    AnyExceptionClass::ManakaiDOM:all
  @rdfs:subClassOf: DISLang|Exception
  @rdfs:subClassOf: ecore|AnyErrorCode

SPropDef:
  @QName: dx|implementedBy
  @enDesc:
    The subject resource, which is an <Q::dx:Interface>,
    is implemented by the object resource, which is a <Q::dx:Class>.
  @rdfs:range: dx|Class
  @rdfs:domain: dx|Interface
  @dataType: DISCore|TFPQNames
  @multipleProperties: DISCore|UnorderedList

ResourceTypeDef:
  @QName: dx|ErrorCode
  @enFN: error code
  @enDesc:
    A <Q::dx:ErrorCode> is a kind of error.
  @rdfs:subClassOf: DISLang|Const
  @rdfs:subClassOf: ecore|AnyErrorCode

ResourceTypeDef:
  @QName: dx|ErrorSubCode
  @FullName:
    @@lang:en
    @@@: Exception/warning subtype
  @Description:
    @@lang:en
    @@@:
      A subtype of an exception or warning.
  @rdfs:subClassOf: ecore|AnyErrorCode

ElementTypeBinding:
  @Name: ResourceTypeDef
  @ElementType: 
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdfs:Class
    @@rdfs:subClassOf: rdfs|Class
    @@For: =ManakaiDOM|all

## -- Throwing of exceptions

SPropDef:
  @QName: dx|raises
  @Description:
    @@lang:en
    @@@:
      An exception the method or attribute might raise.
  @multipleProperties: DISCore|OrderedList
  @dataType: dx|XCRef
  @rdfs:subPropertyOf: ecore|raises

ResourceDef:
  @QName: ManakaiDOM|raises
  @AliasFor: dx|raises
  @For: =ManakaiDOM|all

ElementTypeBinding:
  @Name: PropDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdf:Property
    @@For: =ManakaiDOM|all

ElementTypeBinding:
  @Name: SPropDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      rdf:Property
    @@rdf:type: DISSource|Property
    @@rdf:type: DISSource|ResourceProperty
    @@For: =ManakaiDOM|all

## -- lang:Perl vocabulary

ElementTypeBinding:
  @Name: BlockElementType
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISPerl|BlockElementType
    @@For: =ManakaiDOM|all

LexTypeDef:
  @QName: DISPerl|XWSpecification
  @Description:
    @@lang:en
    @@@:
      A reference to an exception or warning code and 
      optional parameter specification list. 
  @rdfs:subClassOf: DISCore|NSString

BlockElementType:
  @QName:
    disPerl:EXCEPTION
  @Description:
    @@lang:en
    @@@:
      Throws an exception. 
  @dataType: DISPerl|XWSpecification

## -- lang:dis vocabulary

TreeElementType:
  @Name: raiseException
  @Description:
    @@lang:en
    @@@:
      Raises an exception.  Content: Same as <Q::ManakaiDOM:raises>.

ElementTypeBinding:
  @Name: TreeElementType
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:TreeElementType
    @@For: =ManakaiDOM|all

## -- References to Exception

LexTypeDef:
  @QName: dx|XCRef
  @enFN: Exception code reference
  @Description:
    @@lang:en
    @@@:
      A reference to an exception or warning code or subcode. 
      \
      - XCodeRef := FQXCodeRef / coderef / subcoderef
      \
      - FQXCodeRef := xref "." coderef ["." subcoderef]
      \
      - xref := <lt>A dis:TFQNames referring an exception or 
                 warning interface or class>
      \
      - coderef := codeq / codenc
      \
      - codeq := <lt>A dis:TFQNames with a prefixed "Type" QName,
                  referring an exception or warning code>
      \
      - codenc := <lt>An unprefixed DISCore|QName identifying a code 
                   in the null namespace>
      \
      - subcoderef := <lt>A dis:TFQNames referring an exception or warning subcode;
                       it should be prefixed>
  @rdfs:subClassOf: DISCore|NSString

ResourceDef:
  @QName: 
    DOMMain:XCodeRef
  @AliasFor: dx|XCRef
  @For: =ManakaiDOM|all

ElementTypeBinding:
  @Name: LexTypeDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: DISCore|LexicalType
    @@For: =ManakaiDOM|all

## -- Exception classes

XWParam:
  @QName: MDOMX|subtype
  @Type: DISPerl|String
  @enDesc:
    The subtype of the error.

ResourceDef:
  @QName: dx|Exception
  @rdf:type: dx|Class
  @ISA:
    ManakaiDOM:ManakaiDOMExceptionOrWarning
  @For:
    ManakaiDOM:Perl

  @enDesc:
    The <Class::dx|Exception> class is a basis for implementation
    of exceptions thrown in DOM compatible manner.

  @Attr:
    @@Name:text
    @@Description:
      @@@lang:en
      @@@@:
        Returns a short plain-text message, written in the 
        <CODE::i-default> language, that describes 
        what is happened. 
        \
        {NOTE:: This attribute is also defined in the class 
                <PerlModule::Message::Util::Error> and it is 
                overridden here for the purpose of manakai 
                exception and warning reporting. 
        \
        }
        \
        {NOTE:: This attribute only provides a mechanism for programer 
                to know what is occuring; for reporting error to end 
                user, DOM application should implement its own 
                mechanism with localized message.
        \
        }
    @@Get:
      @@@Type:
        DISLang:String::ManakaiDOM:all
      @@@Description:
        @@@@lang:en
        @@@@@:
          An error message written in <CODE::i-default> language.
      @@@PerlDef:
          my $template;
          if (defined $self->{<H::MDOMX:subtype>} and
              defined $self->{<H::ecore:def>}->{<H::MDOMX:subtype>}
                   ->{$self->{<H::MDOMX:subtype>}}
                   ->{<H::ecore:description>}) {
            $template = $self->{<H::ecore:def>}
                   ->{<H::MDOMX:subtype>}
                   ->{$self->{<H::MDOMX:subtype>}}
                   ->{<H::ecore:description>};
          } elsif (defined $self->{<H::ecore:def>}
                   ->{<H::ecore:description>}) {
            $template = $self->{<H::ecore:def>}
                   ->{<H::ecore:description>};
          } else {
            $template = $self->{<H::ecore:type>};
          }
          $r = $self->_FORMATTER_PACKAGE_->new
                    ->replace ($template, param => $self);

  @Method:
    @@Operator:
      @@@@:
        DISPerl:AsStringMethod
      @@@ContentType: DISCore|QName
    @@Description:
      @@@lang:en
      @@@@:
        Generates a simple string that describes what kind of error 
        has occured, written in the <CODE::i-default> language. 
        \
        {NOTE:: The <PerlModule::Message::Util::Error> module defines 
                a generic definition of the method and it is overriden 
                here for the purpose of manakai DOM's exception system. 
        \
        }
    @@Return:
      @@@Type:
        DISLang:String::ManakaiDOM:all
      @@@PerlDef:
        $r = $self->SUPER::stringify;
        if (defined $self->{<H::MDOMX:class>}) {
          my $cls = $self->{<H::MDOMX:class>};
          $cls =~ s/\G.+?:://g;
          if (defined $self->{<H::MDOMX:method>}) {
            $r = $cls . '.' . $self->{<H::MDOMX:method>} . ': ' . $r;
          } elsif (defined $self->{<H::MDOMX:attr>}) {
            $r = $cls . '.' . $self->{<H::MDOMX:attr>} . ' (' .
                 $self->{<H::MDOMX:on>} . '): ' . $r;
          } else {
            $r = $cls . ': ' . $r;
          }
        }
        if (defined $self->{<H::MDOMX:param-name>}) {
          $r = 'Parameter /' . $self->{<H::MDOMX:param-name>} . '/: ' . $r;
        }

  @Attr:
    @@Name: code
    @@enDesc:
      A numeric exception code that identifies the kind of 
      the error.  It should be equal to one of constant values
      defined as members of the exception interface that the
      object implements. 
    @@Type: 
      DOMMain:unsigned-short::ManakaiDOM:all
    @@Get:
      @@@PerlDef:
        $r = $self->{<H::ecore:def>}->{<Q::DOMCore:code>};

  @Attr:
    @@Name: value
    @@Operator:
      @@@@: 0+
      @@@ContentType: lang|Perl
    @@enDesc:
      A numeric exception code that identifies the kind of 
      the error.

      {NOTE:: This attribute always has the same value as 
              <A::dx|Exception.code>.
      }
    @@Type: DOMMain|unsigned-short||ManakaiDOM|all
    @@Get:
      @@@PerlDef:
        $r = $self->{<H::ecore:def>}->{<Q::DOMCore:code>};

  @Attr:
    @@Name: type
    @@enDesc:
      The type of the error, i.e. the local name of the constant
      function whose value is the numeric exception code of the error.
    @@Type: DISLang|String||ManakaiDOM|all
    @@Get:
      @@@PerlDef:
        $r = $self->{<H::ecore:type>};

  @Attr:
    @@Name: subtype
    @@enDesc:
      The subtype of the error.
    @@Type: DISLang|String||ManakaiDOM|all
    @@Get:
      @@@enDesc:
        A URI reference that identifies the subtype.
      @@@InCase:
        @@@@Value:
          @@@@@is-null:1
        @@@@enDesc:
          The error has no subtype information.
      @@@PerlDef:
        $r = $self->{<H::MDOMX:subtype>};
##Exception

ResourceDef:
  @QName: dx|CoreException
  @rdf:type: dx|Class
  @ISA: dx|Exception
  @Description:
    @@lang:en
    @@@:
      Exceptions common to all manakai DOM-style modules. 
  @For:
    ManakaiDOM:Perl
  @ResourceDef:
    @@QName: dx|CoreExceptionCode
    @@rdf:type: DISLang|ConstGroup
    @@Type: idl|unsignedShort||ManakaiDOM|all
    @@rdfs:subClassOf: idl|unsignedShort||ManakaiDOM|all

    @@ResourceDef:
      @@@QName: 
        MDOMX:MDOM_DEBUG_BUG
      @@@rdf:type:
        ManakaiDOM:Const
      @@@Value:
        @@@@@: 3
        @@@@ContentType: DISCore|Integer
      @@@Description:
        @@@@lang:en
        @@@@@:
          Unexpected case occurs.  In general, this exception is not 
          reported.  If the implementation has a bug and 
          something unexpected is occur, this exception is raised. 
        \
          Applications <kwd:MUST-NOT> try to catch this exception. 
          If this exception is raised, please report to the 
          author of that module.
          \
          {NOTE:: New assertion mechanism and subtype <X::DOMMain|ASSERTION_ERR> 
                  has been introduced; it should be used instead of 
                  throwing <X::MDOMX:MDOM_DEBUG_BUG> directly. 
          \
          }
      @@@XParam:
        @@@@QName:
          MDOMX:values
        @@@@Description:
          @@@@@lang:en
          @@@@@@: Arbitrary values for debugging.

      @@@XSubTypeDef:
        @@@@QName: 
          DOMMain:ASSERTION_ERR
        @@@@Description:
          @@@@@lang:en
          @@@@@@:
            Failure to pass an assertion. 
        @@@@XParam:
          @@@@@QName:
            DOMMain:assertionType
          @@@@@Description:
            @@@@@@lang:en
            @@@@@@@:Type of the assertion.
        @@@@XParam:
          @@@@@QName:
            DOMMain:actualValue
          @@@@@Description:
            @@@@@@lang:en
            @@@@@@@: The actual value.
        @@@@XParam:
          @@@@@QName:
            DOMMain:expectedValue
          @@@@@Description:
            @@@@@@lang:en
            @@@@@@@@: The expected value.
        @@@@XParam:
          @@@@@QName:
            DOMMain:expectedLabel
          @@@@@Description:
            @@@@@@lang:en
            @@@@@@@:
              A simple explaination for expected value. 
        @@@@XParam:
          @@@@@QName:
            DOMMain:traceText
          @@@@@Description:
            @@@@@@lang:en
            @@@@@@@:
              Perl's stack trace string (return value of <Perl::Carp::longmess>
              is assumed). 
        @@@@Def:
          @@@@@lang:en
          @@@@@ContentType:
            lang:muf
          @@@@@@:
            ASSERTION ERROR: got "%p (name => {<Q::DOMMain:actualValue>});" 
            while %p (name => {<Q::DOMMain:expectedValue>}, prefix => {"},
            suffix => {"});%p (name => {<Q::DOMMain:expectedLabel>},
            prefix => { (}, suffix => {)}); is expected %p (
            name => {<Q::DOMMain:traceText>}); 

    @@ResourceDef:
      @@@QName: NO_MODIFICATION_ALLOWED_ERR
      @@@rdf:type:
        ManakaiDOM:Const
      @@@Value:
        @@@@@:7
        @@@@ContentType: DISCore|Integer
      @@@Description:
        @@@@lang:en
        @@@@@: An attempt is made to modify read-only object.
      @@@XSubTypeDef:
        @@@@QName: READ_ONLY_ATTRIBUTE_ERR
        @@@@Description:
          @@@@@lang:en
          @@@@@@:
            An attempt is made to modify a read-only attribute.
          @@@@enMufDef:
              Attribute "%p (name => {<Q::MDOMX:class>}, suffix => {.}
              );%p (name => {<Q::MDOMX:attr>});" is read-only
  
    @@ResourceDef:
      @@@QName: NOT_SUPPORTED_ERR
      @@@rdf:type:
        ManakaiDOM:Const
      @@@Value:
        @@@@@:9
        @@@@ContentType: DISCore|Integer
      @@@Description:
        @@@@lang:en
        @@@@@: The implementation does not support the action. 

        @@@XSubTypeDef:
          @@@@QName:
            MDOMX:MDOM_IMPL_ATTR_NOT_IMPLEMENTED
          @@@@Description:
            @@@@@lang:en
            @@@@@@:
              The implementation does not support this attribute.
          @@@@XParam:
            @@@@@QName:
              MDOMX:class
            @@@@@Description:
              @@@@@@lang:en
              @@@@@@@:
                The name of class to which the attribute not implemented belongs.
          @@@@XParam:
            @@@@@QName:
              MDOMX:attr
            @@@@@Description:
              @@@@@@lang:en
              @@@@@@@:
                The name of attribute that is not implemented.
          @@@@XParam:
            @@@@@QName:
              MDOMX:on
            @@@@@InCase:
              @@@@@@Value:
                @@@@@@@@: get
                @@@@@@@ContentType: DISCore|String
              @@@@@@Description:
                @@@@@@@lang:en
                @@@@@@@@: Getting this attribute is not implemented.
            @@@@@InCase:
              @@@@@@Value:
                @@@@@@@@: set
                @@@@@@@ContentType: DISCore|String
              @@@@@@Description:
                @@@@@@@lang:en
                @@@@@@@@: Setting this attribute is not implemented.
          @@@@enMufDef:
              The attribute "%p (name => {<Q::MDOMX:class>}, suffix => {.}
              );%p (name => {<Q::MDOMX:attr>});"
              %p (name => {<Q::MDOMX:on>},
                  prefix => {(}, suffix => {)}); has not been implemented yet 
        @@@XSubTypeDef:
          @@@@QName:
            MDOMX:MDOM_IMPL_METHOD_NOT_IMPLEMENTED
          @@@@Description:
            @@@@@lang:en
            @@@@@@:
              The DOM implementation does not support this method.
          @@@@XParam:
            @@@@@QName:
              MDOMX:class
            @@@@@Description:
              @@@@@@lang:en
              @@@@@@@:
                The name of class to which the method not implemented belongs.
          @@@@XParam:
            @@@@@QName:
              MDOMX:method
            @@@@@Description:
              @@@@@@lang:en
              @@@@@@@:
                The name of method that is not implemented.
          @@@@enMufDef:
              The method "%p (name => {<Q::MDOMX:class>}, suffix => {.}
              );%p (name => {<Q::MDOMX:method>});" has not been implemented yet
##CoreException

ElementTypeBinding:
  @Name: Method
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:Method

ElementTypeBinding:
  @Name: Return
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:MethodReturn

ElementTypeBinding:
  @Name: Param
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:MethodParameter

ElementTypeBinding:
  @Name: Attr
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:Attribute

ElementTypeBinding:
  @Name: Get
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      DISLang:AttributeGet

ElementTypeBinding:
  @Name: InCase
  @ElementType:
    dis:ImplNote
#    dis:ResourceDef
  @ShadowContent:
    @@rdf:type:
      ManakaiDOM:InCase

ElementTypeBinding:
  @Name: PerlDef
  @ElementType:
    dis:Def
  @ShadowContent:
    @@ContentType:
      lang:Perl

ElementTypeBinding:
  @Name:enMufDef
  @ElementType:
    ecore:defaultMessage
  @ShadowContent:
    @@lang:en
    @@ContentType:
      lang:muf

ElementTypeBinding:
  @Name: XSubTypeDef
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: dx|ErrorSubCode

ElementTypeBinding:
  @Name: XParam
  @ElementType:
    ecore:hasParameter

## -- Common exception parameters

XWParam:
  @QName:
    MDOMX:param-name
  @Type: 
    DISPerl:MethodName
  @Description:
    @@lang:en
    @@@:
      A name of method parameter that is related to the exception 
      or warning issued. 

XWParam:
  @QName:
    MDOMX:class
  @Type: DISPerl|String||ManakaiDOM|all
  @Description:
    @@lang:en
    @@@:
      The name of class in whose method or attribute an exception 
      or warning is thrown. 

XWParam:
  @QName: MDOMX|attr
  @Type: DISPerl|String||ManakaiDOM|all
  @enDesc:
    The name of the attribute subroutine in whcih an exception is thrown.

XWParam:
  @QName: MDOMX|on
  @Type: DISPerl|String||ManakaiDOM|all
  @enDesc:
    The type of the attribute operation in which an exception is thrown.
    The value must be either <CODE::get> or <CODE::set>.

XWParam:
  @QName: MDOMX|method
  @Type: DISPerl|String||ManakaiDOM|all
  @enDesc:
    The name of the method subroutine in which an exception is thrown.

XWParam:
  @QName: 
    MDOMX:resourceURI
  @Description:
    @@lang:en
    @@@:
      The URI reference of the resource in which the source code
      is defined.

ElementTypeBinding:
  @Name: XWParam
  @ElementType:
    dis:ResourceDef
  @ShadowContent:
    @@rdf:type: ecore|Parameter
    @@For: =ManakaiDOM|all

## -- Exception target

ResourceDef:
  @QName: ManakaiDefaultExceptionHandler
  @enDesc:
    This class provides a default exception handler.
  @rdf:type: DISLang|Class
  @ForCheck:
    ManakaiDOM:Perl
  @Implement: ecore|MUErrorTarget

  @ResourceDef:
    @@rdf:type:
      DISLang:Method
    @@Operator:
      @@@@:
        ManakaiDOM:MUErrorHandler
      @@@ContentType: DISCore|QName
    @@Description:
      @@@lang:en
      @@@@:
        This method is a generic error handler; all manakai exceptions 
        and warnings associated to an object are once reported to this method 
        and then delivered to appropriate entities. 
        \
        If the error is an exception (such as <X::DOMCore:DOMException> or 
        <X::ManakaiDOM:ManakaiDOMImplementationException>), it is 
        simply thrown.  If the error is only a warning, it is reported 
        via the <IF::DOMCore:DOMError> interface. 
        \
        See <PerlModule::Message::Util::Error> documentation for more 
        information on the error reporting mechanism.
        \
        {NOTE:: This kind of methods are named as <Perl::___report_error> 
                in the convention of <PerlModule::Message::Util::Error>. 
        \
        }
    @@Param:
      @@@Name:err
      @@@Type: ecore|ErrorInterface
      @@@Description:
        @@@@lang:en
        @@@@@:
          An exception object that describes what kind of error it is 
          and how it should be recovered. 
          \
          {NOTE:: Exception objects in manakai implementation 
                  inherit the class <PerlModule::Message::Util::Error>, 
                  that in turn inherits <PerlModule::Error>.
          \
          }
    @@Return:
      @@@PerlDef:
          if ($err->isa (<ClassName::dx|Exception>)) {
            $err-><M::ecore|ErrorInterface.throw>;
          } else {
            warn $err->stringify;
          }
##ManakaiDefaultExceptionHandler
